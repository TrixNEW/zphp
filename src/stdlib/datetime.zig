const std = @import("std");
const Value = @import("../runtime/value.zig").Value;
const PhpObject = @import("../runtime/value.zig").PhpObject;
const vm_mod = @import("../runtime/vm.zig");
const VM = vm_mod.VM;
const NativeResult = vm_mod.NativeResult;
const NativeContext = vm_mod.NativeContext;
const ClassDef = vm_mod.ClassDef;
const Allocator = std.mem.Allocator;
const RuntimeError = error{ RuntimeError, OutOfMemory };
const zlib = @cImport(@cInclude("zlib.h"));

const DT_FORMAT_CONSTS = .{
    .{ "ATOM", "Y-m-d\\TH:i:sP" },
    .{ "COOKIE", "l, d-M-Y H:i:s T" },
    .{ "ISO8601", "Y-m-d\\TH:i:sO" },
    .{ "ISO8601_EXPANDED", "X-m-d\\TH:i:sP" },
    .{ "RFC822", "D, d M y H:i:s O" },
    .{ "RFC850", "l, d-M-y H:i:s T" },
    .{ "RFC1036", "D, d M y H:i:s O" },
    .{ "RFC1123", "D, d M Y H:i:s O" },
    .{ "RFC2822", "D, d M Y H:i:s O" },
    .{ "RFC3339", "Y-m-d\\TH:i:sP" },
    .{ "RFC3339_EXTENDED", "Y-m-d\\TH:i:s.vP" },
    .{ "RFC7231", "D, d M Y H:i:s \\G\\M\\T" },
    .{ "RSS", "D, d M Y H:i:s O" },
    .{ "W3C", "Y-m-d\\TH:i:sP" },
};

pub const entries = .{
    .{ "date", native_date },
    .{ "date_create", native_date_create },
    .{ "date_create_immutable", native_date_create_immutable },
    .{ "date_create_from_format", native_date_create_from_format },
    .{ "date_create_immutable_from_format", native_date_create_immutable_from_format },
    .{ "date_format", native_date_format },
    .{ "date_modify", native_date_modify },
    .{ "date_add", native_date_add },
    .{ "date_sub", native_date_sub },
    .{ "date_diff", native_date_diff },
    .{ "date_timestamp_get", native_date_timestamp_get },
    .{ "date_timestamp_set", native_date_timestamp_set },
    .{ "date_date_set", native_date_date_set },
    .{ "date_time_set", native_date_time_set },
    .{ "date_parse", native_date_parse },
    .{ "date_parse_from_format", native_date_parse_from_format },
    .{ "date_get_last_errors", dtGetLastErrors },
    .{ "mktime", native_mktime },
    .{ "gmmktime", native_gmmktime },
    .{ "strtotime", native_strtotime },
    .{ "time", native_time },
    .{ "microtime", native_microtime },
    .{ "hrtime", native_hrtime },
    .{ "checkdate", native_checkdate },
    .{ "cal_days_in_month", native_cal_days_in_month },
    .{ "getdate", native_getdate },
    .{ "gmdate", native_gmdate },
    .{ "date_default_timezone_set", native_tz_set },
    .{ "date_default_timezone_get", native_tz_get },
    .{ "timezone_identifiers_list", dtzListIdentifiers },
    .{ "timezone_abbreviations_list", dtzListAbbreviations },
    .{ "timezone_name_get", native_timezone_name_get },
    .{ "timezone_offset_get", native_timezone_offset_get },
    .{ "timezone_open", native_timezone_open },
    .{ "date_timezone_get", native_date_timezone_get },
    .{ "date_timezone_set", native_date_timezone_set },
    .{ "localtime", native_localtime },
    .{ "idate", native_idate },
    .{ "date_interval_create_from_date_string", native_date_interval_create_from_date_string },
    .{ "date_interval_format", native_date_interval_format },
    .{ "date_offset_get", methodAlias("getOffset") },
    .{ "date_isodate_set", methodAlias("setISODate") },
    .{ "timezone_transitions_get", methodAlias("getTransitions") },
    .{ "timezone_location_get", methodAlias("getLocation") },
};

pub fn register(vm: *VM, a: Allocator) !void {
    // DateTimeInterface
    var iface = vm_mod.InterfaceDef{ .name = "DateTimeInterface" };
    try iface.methods.append(a, "format");
    try iface.methods.append(a, "getTimestamp");
    try vm.interfaces.put(a, "DateTimeInterface", iface);

    // shadow class so DateTimeInterface::ATOM-style constant lookups resolve
    var dti_const = ClassDef{ .name = "DateTimeInterface", .is_abstract = true };
    inline for (DT_FORMAT_CONSTS) |c| {
        try dti_const.constants.put(a, c[0], .{ .string = Value.String.borrowed(c[1]) });
    }
    try vm.classes.put(a, "DateTimeInterface", dti_const);

    // DateTime class
    var dt_def = ClassDef{ .name = "DateTime" };
    try dt_def.properties.append(a, .{ .name = "timestamp", .default = .{ .int = 0 }, .visibility = .private });
    try dt_def.interfaces.append(a, "DateTimeInterface");
    try dt_def.methods.put(a, "__construct", .{ .name = "__construct", .arity = 1 });
    try dt_def.methods.put(a, "format", .{ .name = "format", .arity = 1 });
    try dt_def.methods.put(a, "getTimestamp", .{ .name = "getTimestamp", .arity = 0 });
    try dt_def.methods.put(a, "setTimestamp", .{ .name = "setTimestamp", .arity = 1 });
    try dt_def.methods.put(a, "modify", .{ .name = "modify", .arity = 1 });
    try dt_def.methods.put(a, "add", .{ .name = "add", .arity = 1 });
    try dt_def.methods.put(a, "sub", .{ .name = "sub", .arity = 1 });
    try dt_def.methods.put(a, "diff", .{ .name = "diff", .arity = 1 });
    try dt_def.methods.put(a, "setDate", .{ .name = "setDate", .arity = 3 });
    try dt_def.methods.put(a, "setTime", .{ .name = "setTime", .arity = 4 });
    try dt_def.methods.put(a, "setISODate", .{ .name = "setISODate", .arity = 2 });
    try dt_def.methods.put(a, "createFromTimestamp", .{ .name = "createFromTimestamp", .arity = 1, .is_static = true });
    try dt_def.methods.put(a, "createFromFormat", .{ .name = "createFromFormat", .arity = 2, .is_static = true });
    try dt_def.methods.put(a, "getMicrosecond", .{ .name = "getMicrosecond", .arity = 0 });
    try dt_def.methods.put(a, "setMicrosecond", .{ .name = "setMicrosecond", .arity = 1 });
    try dt_def.methods.put(a, "getLastErrors", .{ .name = "getLastErrors", .arity = 0, .is_static = true });
    try dt_def.methods.put(a, "getTimezone", .{ .name = "getTimezone", .arity = 0 });
    try dt_def.methods.put(a, "setTimezone", .{ .name = "setTimezone", .arity = 1 });
    try dt_def.methods.put(a, "getOffset", .{ .name = "getOffset", .arity = 0 });
    try dt_def.methods.put(a, "createFromImmutable", .{ .name = "createFromImmutable", .arity = 1, .is_static = true });
    try dt_def.methods.put(a, "createFromInterface", .{ .name = "createFromInterface", .arity = 1, .is_static = true });
    inline for (DT_FORMAT_CONSTS) |c| {
        try dt_def.constants.put(a, c[0], .{ .string = Value.String.borrowed(c[1]) });
    }
    try vm.classes.put(a, "DateTime", dt_def);

    try vm.native_fns.put(a, "DateTime::__construct", dtConstruct);
    try vm.native_fns.put(a, "DateTime::format", dtFormat);
    try vm.native_fns.put(a, "DateTime::getTimestamp", dtGetTimestamp);
    try vm.native_fns.put(a, "DateTime::setTimestamp", dtSetTimestamp);
    try vm.native_fns.put(a, "DateTime::modify", dtModify);
    try vm.native_fns.put(a, "DateTime::add", dtAdd);
    try vm.native_fns.put(a, "DateTime::sub", dtSub);
    try vm.native_fns.put(a, "DateTime::diff", dtDiff);
    try vm.native_fns.put(a, "DateTime::setDate", dtSetDate);
    try vm.native_fns.put(a, "DateTime::setTime", dtSetTime);
    try vm.native_fns.put(a, "DateTime::setISODate", dtSetISODate);
    try vm.native_fns.put(a, "DateTime::createFromTimestamp", dtCreateFromTimestamp);
    try vm.native_fns.put(a, "DateTime::createFromFormat", dtCreateFromFormat);
    try vm.native_fns.put(a, "DateTime::getMicrosecond", dtGetMicrosecond);
    try vm.native_fns.put(a, "DateTime::setMicrosecond", dtSetMicrosecond);
    try vm.native_fns.put(a, "DateTime::getLastErrors", dtGetLastErrors);
    try vm.native_fns.put(a, "DateTime::getTimezone", dtGetTimezone);
    try vm.native_fns.put(a, "DateTime::setTimezone", dtSetTimezone);
    try vm.native_fns.put(a, "DateTime::getOffset", dtGetOffset);
    try vm.native_fns.put(a, "DateTimeImmutable::getOffset", dtGetOffset);

    // DateTimeImmutable
    var dti_def = ClassDef{ .name = "DateTimeImmutable" };
    try dti_def.properties.append(a, .{ .name = "timestamp", .default = .{ .int = 0 }, .visibility = .private });
    try dti_def.interfaces.append(a, "DateTimeInterface");
    try dti_def.methods.put(a, "__construct", .{ .name = "__construct", .arity = 1 });
    try dti_def.methods.put(a, "format", .{ .name = "format", .arity = 1 });
    try dti_def.methods.put(a, "getTimestamp", .{ .name = "getTimestamp", .arity = 0 });
    try dti_def.methods.put(a, "modify", .{ .name = "modify", .arity = 1 });
    try dti_def.methods.put(a, "add", .{ .name = "add", .arity = 1 });
    try dti_def.methods.put(a, "sub", .{ .name = "sub", .arity = 1 });
    try dti_def.methods.put(a, "diff", .{ .name = "diff", .arity = 1 });
    try dti_def.methods.put(a, "createFromTimestamp", .{ .name = "createFromTimestamp", .arity = 1, .is_static = true });
    try dti_def.methods.put(a, "getMicrosecond", .{ .name = "getMicrosecond", .arity = 0 });
    try dti_def.methods.put(a, "setMicrosecond", .{ .name = "setMicrosecond", .arity = 1 });
    try dti_def.methods.put(a, "getLastErrors", .{ .name = "getLastErrors", .arity = 0, .is_static = true });
    try dti_def.methods.put(a, "getTimezone", .{ .name = "getTimezone", .arity = 0 });
    try dti_def.methods.put(a, "setTimezone", .{ .name = "setTimezone", .arity = 1 });
    try dti_def.methods.put(a, "getOffset", .{ .name = "getOffset", .arity = 0 });
    try dti_def.methods.put(a, "createFromFormat", .{ .name = "createFromFormat", .arity = 2, .is_static = true });
    try dti_def.methods.put(a, "createFromMutable", .{ .name = "createFromMutable", .arity = 1, .is_static = true });
    try dti_def.methods.put(a, "createFromInterface", .{ .name = "createFromInterface", .arity = 1, .is_static = true });
    try dti_def.methods.put(a, "setISODate", .{ .name = "setISODate", .arity = 2 });
    inline for (DT_FORMAT_CONSTS) |c| {
        try dti_def.constants.put(a, c[0], .{ .string = Value.String.borrowed(c[1]) });
    }
    try vm.classes.put(a, "DateTimeImmutable", dti_def);

    try vm.native_fns.put(a, "DateTimeImmutable::__construct", dtConstruct);
    try vm.native_fns.put(a, "DateTimeImmutable::format", dtFormat);
    try vm.native_fns.put(a, "DateTimeImmutable::getTimestamp", dtGetTimestamp);
    try vm.native_fns.put(a, "DateTimeImmutable::modify", dtiModify);
    try vm.native_fns.put(a, "DateTimeImmutable::add", dtiAdd);
    try vm.native_fns.put(a, "DateTimeImmutable::sub", dtiSub);
    try vm.native_fns.put(a, "DateTimeImmutable::diff", dtDiff);
    try vm.native_fns.put(a, "DateTimeImmutable::createFromTimestamp", dtiCreateFromTimestamp);
    try vm.native_fns.put(a, "DateTimeImmutable::getMicrosecond", dtGetMicrosecond);
    try vm.native_fns.put(a, "DateTimeImmutable::setMicrosecond", dtiSetMicrosecond);
    try vm.native_fns.put(a, "DateTimeImmutable::getLastErrors", dtGetLastErrors);
    try vm.native_fns.put(a, "DateTimeImmutable::getTimezone", dtGetTimezone);
    try vm.native_fns.put(a, "DateTimeImmutable::setTimezone", dtiSetTimezone);
    try vm.native_fns.put(a, "DateTimeImmutable::createFromFormat", dtiCreateFromFormat);
    try vm.native_fns.put(a, "DateTimeImmutable::setDate", dtiSetDate);
    try vm.native_fns.put(a, "DateTimeImmutable::setTime", dtiSetTime);
    try vm.native_fns.put(a, "DateTimeImmutable::setISODate", dtiSetISODate);
    try vm.native_fns.put(a, "DateTimeImmutable::setTimestamp", dtiSetTimestamp);
    try vm.native_fns.put(a, "DateTimeImmutable::createFromMutable", dtiCreateFromMutable);
    try vm.native_fns.put(a, "DateTime::createFromImmutable", dtCreateFromImmutable);
    try vm.native_fns.put(a, "DateTime::createFromInterface", dtCreateFromInterface);
    try vm.native_fns.put(a, "DateTimeImmutable::createFromInterface", dtiCreateFromInterface);

    // DateTimeZone class
    var dtz_def = ClassDef{ .name = "DateTimeZone" };
    try dtz_def.properties.append(a, .{ .name = "timezone", .default = .{ .string = Value.String.borrowed("UTC") }, .visibility = .private });
    try dtz_def.methods.put(a, "__construct", .{ .name = "__construct", .arity = 1 });
    try dtz_def.methods.put(a, "getName", .{ .name = "getName", .arity = 0 });
    try dtz_def.methods.put(a, "getOffset", .{ .name = "getOffset", .arity = 1 });
    try dtz_def.methods.put(a, "__toString", .{ .name = "__toString", .arity = 0 });
    try dtz_def.methods.put(a, "listIdentifiers", .{ .name = "listIdentifiers", .arity = 2, .is_static = true });
    try dtz_def.methods.put(a, "listAbbreviations", .{ .name = "listAbbreviations", .arity = 0, .is_static = true });
    try dtz_def.methods.put(a, "getLocation", .{ .name = "getLocation", .arity = 0 });
    try dtz_def.methods.put(a, "getTransitions", .{ .name = "getTransitions", .arity = 2 });
    try vm.classes.put(a, "DateTimeZone", dtz_def);

    try vm.native_fns.put(a, "DateTimeZone::__construct", dtzConstruct);
    try vm.native_fns.put(a, "DateTimeZone::getName", dtzGetName);
    try vm.native_fns.put(a, "DateTimeZone::getOffset", dtzGetOffset);
    try vm.native_fns.put(a, "DateTimeZone::__toString", dtzGetName);
    try vm.native_fns.put(a, "DateTimeZone::listIdentifiers", dtzListIdentifiers);
    try vm.native_fns.put(a, "DateTimeZone::listAbbreviations", dtzListAbbreviations);
    try vm.native_fns.put(a, "DateTimeZone::getLocation", dtzGetLocation);
    try vm.native_fns.put(a, "DateTimeZone::getTransitions", dtzGetTransitions);

    // DateInterval
    var di_def = ClassDef{ .name = "DateInterval" };
    try di_def.properties.append(a, .{ .name = "y", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "m", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "d", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "h", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "i", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "s", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "f", .default = .{ .float = 0.0 } });
    try di_def.properties.append(a, .{ .name = "days", .default = .{ .int = 0 } });
    try di_def.properties.append(a, .{ .name = "invert", .default = .{ .int = 0 } });
    try di_def.methods.put(a, "__construct", .{ .name = "__construct", .arity = 1 });
    try di_def.methods.put(a, "invert", .{ .name = "invert", .arity = 1 });
    try vm.classes.put(a, "DateInterval", di_def);

    try vm.native_fns.put(a, "DateInterval::__construct", diConstruct);
    try vm.native_fns.put(a, "DateInterval::invert", diInvert);
    try vm.native_fns.put(a, "DateInterval::createFromDateString", diCreateFromDateString);
    try vm.native_fns.put(a, "DateInterval::format", diFormat);

    // DatePeriod
    var dp_def = ClassDef{ .name = "DatePeriod" };
    try dp_def.constants.put(a, "EXCLUDE_START_DATE", .{ .int = 1 });
    try dp_def.constants.put(a, "INCLUDE_END_DATE", .{ .int = 2 });
    try dp_def.interfaces.append(a, "IteratorAggregate");
    try dp_def.methods.put(a, "__construct", .{ .name = "__construct", .arity = 3 });
    try dp_def.methods.put(a, "getStartDate", .{ .name = "getStartDate", .arity = 0 });
    try dp_def.methods.put(a, "getEndDate", .{ .name = "getEndDate", .arity = 0 });
    try dp_def.methods.put(a, "getDateInterval", .{ .name = "getDateInterval", .arity = 0 });
    try dp_def.methods.put(a, "getRecurrences", .{ .name = "getRecurrences", .arity = 0 });
    try dp_def.methods.put(a, "getIterator", .{ .name = "getIterator", .arity = 0 });
    try vm.classes.put(a, "DatePeriod", dp_def);
    try vm.native_fns.put(a, "DatePeriod::__construct", dpConstruct);
    try vm.native_fns.put(a, "DatePeriod::getStartDate", dpGetStart);
    try vm.native_fns.put(a, "DatePeriod::getEndDate", dpGetEnd);
    try vm.native_fns.put(a, "DatePeriod::getDateInterval", dpGetInterval);
    try vm.native_fns.put(a, "DatePeriod::getRecurrences", dpGetRecurrences);
    try vm.native_fns.put(a, "DatePeriod::getIterator", dpGetIterator);

    var dpi_def = ClassDef{ .name = "DatePeriodIterator" };
    try dpi_def.interfaces.append(a, "Iterator");
    try dpi_def.methods.put(a, "current", .{ .name = "current", .arity = 0 });
    try dpi_def.methods.put(a, "key", .{ .name = "key", .arity = 0 });
    try dpi_def.methods.put(a, "next", .{ .name = "next", .arity = 0 });
    try dpi_def.methods.put(a, "rewind", .{ .name = "rewind", .arity = 0 });
    try dpi_def.methods.put(a, "valid", .{ .name = "valid", .arity = 0 });
    try vm.classes.put(a, "DatePeriodIterator", dpi_def);
    try vm.native_fns.put(a, "DatePeriodIterator::current", dpiCurrent);
    try vm.native_fns.put(a, "DatePeriodIterator::key", dpiKey);
    try vm.native_fns.put(a, "DatePeriodIterator::next", dpiNext);
    try vm.native_fns.put(a, "DatePeriodIterator::rewind", dpiRewind);
    try vm.native_fns.put(a, "DatePeriodIterator::valid", dpiValid);
}

// parse a "YYYY-MM-DD" or "YYYY-MM-DDTHH:MM:SS" (optional trailing Z) date
// string to a unix timestamp interpreted as UTC. used by the ISO 8601
// DatePeriod string form
fn parseIsoDateToTs(s: []const u8) ?i64 {
    if (s.len < 10 or s[4] != '-' or s[7] != '-') return null;
    const year = std.fmt.parseInt(i64, s[0..4], 10) catch return null;
    const month = std.fmt.parseInt(i64, s[5..7], 10) catch return null;
    const day = std.fmt.parseInt(i64, s[8..10], 10) catch return null;
    var hour: i64 = 0;
    var min: i64 = 0;
    var sec: i64 = 0;
    if (s.len >= 19 and (s[10] == 'T' or s[10] == ' ') and s[13] == ':' and s[16] == ':') {
        hour = std.fmt.parseInt(i64, s[11..13], 10) catch 0;
        min = std.fmt.parseInt(i64, s[14..16], 10) catch 0;
        sec = std.fmt.parseInt(i64, s[17..19], 10) catch 0;
    }
    return dateToTimestamp(year, month, day, hour, min, sec);
}

fn dpConstruct(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);

    // ISO 8601 recurring-interval string form: "R<n>/<start>/<interval>"
    // e.g. new DatePeriod('R3/2024-01-01T00:00:00Z/P1D'). PHP's string
    // constructor only accepts the R-prefixed recurring form; the bare
    // start/interval/end string is a DateMalformedPeriodStringException there
    if (args.len >= 1 and args[0] == .string) {
        const spec = args[0].string.bytes();
        var it = std.mem.splitScalar(u8, spec, '/');
        const p0 = it.next() orelse return NativeResult.scalar(.null);
        const p1 = it.next() orelse return NativeResult.scalar(.null);
        const p2 = it.next() orelse return NativeResult.scalar(.null);
        if (p0.len < 2 or (p0[0] != 'R' and p0[0] != 'r')) return NativeResult.scalar(.null);
        const recurrences = std.fmt.parseInt(i64, p0[1..], 10) catch 0;
        const start_ts = parseIsoDateToTs(p1) orelse return NativeResult.scalar(.null);
        const start_obj = try ctx.createObject("DateTime");
        try start_obj.set(ctx.allocator, "timestamp", .{ .int = start_ts });
        try obj.set(ctx.allocator, "__start", .{ .object = start_obj });

        const dur = parseIsoDuration(p2);
        const di_obj = try ctx.createObject("DateInterval");
        try di_obj.set(ctx.allocator, "y", .{ .int = dur.y });
        try di_obj.set(ctx.allocator, "m", .{ .int = dur.m });
        try di_obj.set(ctx.allocator, "d", .{ .int = dur.d });
        try di_obj.set(ctx.allocator, "h", .{ .int = dur.h });
        try di_obj.set(ctx.allocator, "i", .{ .int = dur.mi });
        try di_obj.set(ctx.allocator, "s", .{ .int = dur.s });
        try di_obj.set(ctx.allocator, "f", .{ .float = dur.f });
        try di_obj.set(ctx.allocator, "invert", .{ .int = 0 });
        try di_obj.set(ctx.allocator, "days", .{ .bool = false });
        try obj.set(ctx.allocator, "__interval", .{ .object = di_obj });

        try obj.set(ctx.allocator, "__recurrences", .{ .int = recurrences });
        if (args.len >= 2 and args[1] == .int) try obj.set(ctx.allocator, "__options", args[1]);
        return NativeResult.scalar(.null);
    }

    if (args.len < 3) return NativeResult.scalar(.null);
    if (args[0] != .object or args[1] != .object) return NativeResult.scalar(.null);
    try obj.set(ctx.allocator, "__start", args[0]);
    try obj.set(ctx.allocator, "__interval", args[1]);
    if (args[2] == .object) {
        try obj.set(ctx.allocator, "__end", args[2]);
    } else if (args[2] == .int) {
        try obj.set(ctx.allocator, "__recurrences", args[2]);
    }
    if (args.len >= 4) try obj.set(ctx.allocator, "__options", args[3]);
    return NativeResult.scalar(.null);
}

fn dpGetStart(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    return NativeResult.borrowed(obj.get("__start"));
}

fn dpGetEnd(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    return NativeResult.borrowed(obj.get("__end"));
}

fn dpGetInterval(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    return NativeResult.borrowed(obj.get("__interval"));
}

fn dpGetRecurrences(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    // explicitly stored recurrence count (third constructor arg was an int).
    // when constructed with an end-DateTime instead, PHP returns null
    const rec = obj.get("__recurrences");
    if (rec == .int) return NativeResult.scalar(rec);
    return NativeResult.scalar(.null);
}

fn dpGetIterator(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    const iter = try ctx.vm.allocator.create(@import("../runtime/value.zig").PhpObject);
    iter.* = .{ .class_name = "DatePeriodIterator" };
    try ctx.vm.objects.append(ctx.vm.allocator, iter);
    try iter.set(ctx.allocator, "__start", obj.get("__start"));
    try iter.set(ctx.allocator, "__end", obj.get("__end"));
    try iter.set(ctx.allocator, "__interval", obj.get("__interval"));
    try iter.set(ctx.allocator, "__recurrences", obj.get("__recurrences"));
    const opts = obj.get("__options");
    const exclude_start = opts == .int and (opts.int & 1) != 0;
    const include_end = opts == .int and (opts.int & 2) != 0;
    const start_v = obj.get("__start");
    if (start_v != .object) return NativeResult.scalar(.null);
    var ts = getTimestamp(start_v.object);
    if (exclude_start) {
        const di = obj.get("__interval");
        if (di == .object) {
            const tz_name = objTzName(start_v.object, ctx.vm.default_tz_name);
            ts = applyIntervalTz(ctx.allocator, ts, di.object, 1, tz_name);
        }
    }
    try iter.set(ctx.allocator, "__cursor_ts", .{ .int = ts });
    try iter.set(ctx.allocator, "__index", .{ .int = 0 });
    try iter.set(ctx.allocator, "__exclude_start", .{ .bool = exclude_start });
    try iter.set(ctx.allocator, "__include_end", .{ .bool = include_end });
    return NativeResult.borrowed(.{ .object = iter });
}

fn dpiTimestampInRange(this: *@import("../runtime/value.zig").PhpObject) bool {
    const ts = Value.toInt(this.get("__cursor_ts"));
    const end_v = this.get("__end");
    if (end_v == .object) {
        const end_ts = getTimestamp(end_v.object);
        const include_end = this.get("__include_end") == .bool and this.get("__include_end").bool;
        return if (include_end) ts <= end_ts else ts < end_ts;
    }
    const rec_v = this.get("__recurrences");
    if (rec_v == .int) {
        const exclude_start = this.get("__exclude_start") == .bool and this.get("__exclude_start").bool;
        const idx = Value.toInt(this.get("__index"));
        return if (exclude_start) idx < rec_v.int else idx <= rec_v.int;
    }
    return false;
}

fn dpiCurrent(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const this = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (!dpiTimestampInRange(this)) return NativeResult.scalar(.{ .bool = false });
    const ts = Value.toInt(this.get("__cursor_ts"));
    const dt = try ctx.createObject("DateTime");
    try dt.set(ctx.allocator, "timestamp", .{ .int = ts });
    const start_v = this.get("__start");
    if (start_v == .object) {
        const tz = start_v.object.get("__timezone");
        if (tz == .string) try dt.set(ctx.allocator, "__timezone", tz);
    }
    return NativeResult.borrowed(.{ .object = dt });
}

fn dpiKey(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const this = getThis(ctx) orelse return NativeResult.scalar(.null);
    return NativeResult.scalar(this.get("__index"));
}

fn dpiNext(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const this = getThis(ctx) orelse return NativeResult.scalar(.null);
    const di = this.get("__interval");
    if (di == .object) {
        const cur = Value.toInt(this.get("__cursor_ts"));
        const start_v = this.get("__start");
        const tz_name = if (start_v == .object) objTzName(start_v.object, ctx.vm.default_tz_name) else ctx.vm.default_tz_name;
        try this.set(ctx.allocator, "__cursor_ts", .{ .int = applyIntervalTz(ctx.allocator, cur, di.object, 1, tz_name) });
    }
    const idx = Value.toInt(this.get("__index"));
    try this.set(ctx.allocator, "__index", .{ .int = idx + 1 });
    return NativeResult.scalar(.null);
}

fn dpiRewind(_: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    return NativeResult.scalar(.null);
}

fn dpiValid(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const this = getThis(ctx) orelse return NativeResult.scalar(.{ .bool = false });
    return NativeResult.scalar(.{ .bool = dpiTimestampInRange(this) });
}

fn dtGetLastErrors(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    // false when the last parse had neither warnings nor errors (php 8.2+)
    if (!ctx.vm.last_dt.set) return NativeResult.scalar(.{ .bool = false });
    const result = try ctx.createArray();
    try putErrorContainer(ctx, result, &ctx.vm.last_dt);
    return NativeResult.borrowed(.{ .array = result });
}

fn getThis(ctx: *NativeContext) ?*PhpObject {
    const v = ctx.vm.currentFrame().vars.get("$this") orelse return null;
    if (v != .object) return null;
    return v.object;
}

fn getTimestamp(obj: *PhpObject) i64 {
    return Value.toInt(obj.get("timestamp"));
}

fn dtConstruct(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    const input: []const u8 = if (args.len >= 1 and args[0] == .string) args[0].string.bytes() else "";
    const instant = (try resolveDate(ctx, input, tzArgName(args, 1), true)) orelse return error.RuntimeError;
    try storeInstant(ctx, obj, instant.ts, instant.us, &instant.zone);
    return NativeResult.scalar(.null);
}

// the zone name of an optional DateTimeZone argument
fn tzArgName(args: []const Value, idx: usize) ?[]const u8 {
    if (args.len <= idx or args[idx] != .object) return null;
    const v = args[idx].object.get("timezone");
    return if (v == .string) v.string.bytes() else null;
}

// a copy of a date object of the same class, for the immutable methods
fn cloneDateObject(ctx: *NativeContext, src: *PhpObject) RuntimeError!*PhpObject {
    const copy = try ctx.createObject(src.class_name);
    if (src.slots) |ss| if (copy.slots) |cs| if (cs.len == ss.len) {
        for (ss, 0..) |v, i| cs[i] = try ctx.vm.copyValue(v);
    };
    var it = src.properties.iterator();
    while (it.next()) |e| try copy.set(ctx.allocator, e.key_ptr.*, e.value_ptr.*);
    return copy;
}

fn dtFormat(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len == 0 or args[0] != .string) return NativeResult.literal("");
    const ts = getTimestamp(obj);
    const tz_val = obj.get("__timezone");
    const tz_name = if (tz_val == .string) tz_val.string.bytes() else ctx.vm.default_tz_name;
    const offset = tzOffsetForName(ctx.allocator, tz_name, ts);
    const us_v = obj.get("__microseconds");
    const us: i64 = if (us_v == .int) us_v.int else 0;
    return formatTimestampTzMicros(ctx, ts, args[0].string.bytes(), offset, tz_name, us);
}

pub fn formatTimestamp(ctx: *NativeContext, timestamp: i64, format: []const u8) RuntimeError!NativeResult {
    const tz_name = ctx.vm.default_tz_name;
    const offset = tzOffsetForName(ctx.allocator, tz_name, timestamp);
    return formatTimestampTzMicros(ctx, timestamp, format, offset, tz_name, 0);
}

pub fn formatTimestampTz(ctx: *NativeContext, timestamp: i64, format: []const u8, tz_offset: i32, tz_name: []const u8) RuntimeError!NativeResult {
    return formatTimestampTzMicros(ctx, timestamp, format, tz_offset, tz_name, 0);
}

// the year as 'c' and 'r' print it: four characters with the sign counted,
// so -2 is "-002"
fn paddedYear(buf: *[24]u8, year: i64) []const u8 {
    if (year >= 0) return std.fmt.bufPrint(buf, "{d:0>4}", .{@as(u64, @intCast(year))}) catch "0000";
    return std.fmt.bufPrint(buf, "-{d:0>3}", .{@abs(year)}) catch "-000";
}

pub fn formatTimestampTzMicros(ctx: *NativeContext, timestamp: i64, format: []const u8, tz_offset: i32, tz_name: []const u8, microseconds: i64) RuntimeError!NativeResult {
    // wraps at the ends of the range, as php's c arithmetic does
    const local_ts = timestamp +% @as(i64, tz_offset);
    const dc = baseComponents(local_ts);
    const day_seconds = FmtDaySec{ .h = @intCast(dc.hour), .mi = @intCast(dc.min), .s = @intCast(dc.sec) };
    const epoch_day = FmtEpochDay{ .day = @divFloor(local_ts, 86400) };
    const year_day = FmtYearDay{ .year = dc.year };
    const month_day = FmtMonthDay{ .month = .{ .v = @intCast(dc.month) }, .day_index = @intCast(dc.day - 1) };
    const a = ctx.allocator;

    var buf = std.ArrayListUnmanaged(u8){};
    defer buf.deinit(a);
    var fi: usize = 0;
    while (fi < format.len) : (fi += 1) {
        const c = format[fi];
        switch (c) {
            'Y' => {
                // at least four digits, with the sign outside the padding
                try buf.writer(a).print("{s}{d:0>4}", .{ if (year_day.year < 0) "-" else "", @abs(year_day.year) });
            },
            'm' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{month_day.month.numeric()}) catch "00";
                try buf.appendSlice(a, s);
            },
            'd' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{month_day.day_index + 1}) catch "00";
                try buf.appendSlice(a, s);
            },
            'H' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{day_seconds.getHoursIntoDay()}) catch "00";
                try buf.appendSlice(a, s);
            },
            'i' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{day_seconds.getMinutesIntoHour()}) catch "00";
                try buf.appendSlice(a, s);
            },
            's' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{day_seconds.getSecondsIntoMinute()}) catch "00";
                try buf.appendSlice(a, s);
            },
            'U' => {
                var tmp: [16]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{timestamp}) catch "0";
                try buf.appendSlice(a, s);
            },
            'N' => {
                const day_num: i64 = @intCast(epoch_day.day);
                const dow: u8 = @intCast(@mod(day_num + 3, 7) + 1);
                var tmp: [2]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{dow}) catch "0";
                try buf.appendSlice(a, s);
            },
            'j' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{month_day.day_index + 1}) catch "0";
                try buf.appendSlice(a, s);
            },
            'n' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{month_day.month.numeric()}) catch "0";
                try buf.appendSlice(a, s);
            },
            'G' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{day_seconds.getHoursIntoDay()}) catch "0";
                try buf.appendSlice(a, s);
            },
            'g' => {
                const h = day_seconds.getHoursIntoDay();
                const h12: u32 = if (h == 0) 12 else if (h > 12) h - 12 else h;
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{h12}) catch "0";
                try buf.appendSlice(a, s);
            },
            'h' => {
                const h = day_seconds.getHoursIntoDay();
                const h12: u32 = if (h == 0) 12 else if (h > 12) h - 12 else h;
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{h12}) catch "00";
                try buf.appendSlice(a, s);
            },
            'A' => try buf.appendSlice(a, if (day_seconds.getHoursIntoDay() < 12) "AM" else "PM"),
            'a' => try buf.appendSlice(a, if (day_seconds.getHoursIntoDay() < 12) "am" else "pm"),
            'l' => {
                const day_num: i64 = @intCast(epoch_day.day);
                const dow: usize = @intCast(@mod(day_num + 3, 7));
                const names = [_][]const u8{ "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday", "Sunday" };
                try buf.appendSlice(a, names[dow]);
            },
            'D' => {
                const day_num: i64 = @intCast(epoch_day.day);
                const dow: usize = @intCast(@mod(day_num + 3, 7));
                const names = [_][]const u8{ "Mon", "Tue", "Wed", "Thu", "Fri", "Sat", "Sun" };
                try buf.appendSlice(a, names[dow]);
            },
            'F' => {
                const names = [_][]const u8{ "January", "February", "March", "April", "May", "June", "July", "August", "September", "October", "November", "December" };
                try buf.appendSlice(a, names[month_day.month.numeric() - 1]);
            },
            'M' => {
                const names = [_][]const u8{ "Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec" };
                try buf.appendSlice(a, names[month_day.month.numeric() - 1]);
            },
            'y' => {
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{@as(u32, @intCast(@mod(year_day.year, 100)))}) catch "00";
                try buf.appendSlice(a, s);
            },
            't' => {
                try buf.writer(a).print("{d}", .{daysInMonth(month_day.month.numeric(), year_day.year)});
            },
            'c' => {
                // ISO 8601: YYYY-MM-DDTHH:MM:SS+00:00
                var tmp: [32]u8 = undefined;
                var ybuf: [24]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{s}-{d:0>2}-{d:0>2}T{d:0>2}:{d:0>2}:{d:0>2}", .{
                    paddedYear(&ybuf, year_day.year),
                    month_day.month.numeric(),
                    month_day.day_index + 1,
                    day_seconds.getHoursIntoDay(),
                    day_seconds.getMinutesIntoHour(),
                    day_seconds.getSecondsIntoMinute(),
                }) catch "0000-00-00T00:00:00";
                try buf.appendSlice(a, s);
                try appendOffsetColon(&buf, a, tz_offset);
            },
            'r' => {
                const day_num: i64 = @intCast(epoch_day.day);
                const dow: usize = @intCast(@mod(day_num + 3, 7));
                const day_names = [_][]const u8{ "Mon", "Tue", "Wed", "Thu", "Fri", "Sat", "Sun" };
                const mon_names = [_][]const u8{ "Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec" };
                try buf.appendSlice(a, day_names[dow]);
                try buf.appendSlice(a, ", ");
                var tmp: [32]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2} ", .{month_day.day_index + 1}) catch "01 ";
                try buf.appendSlice(a, s);
                try buf.appendSlice(a, mon_names[month_day.month.numeric() - 1]);
                var ybuf: [24]u8 = undefined;
                const s2 = std.fmt.bufPrint(&tmp, " {s} {d:0>2}:{d:0>2}:{d:0>2} ", .{
                    paddedYear(&ybuf, year_day.year),
                    day_seconds.getHoursIntoDay(),
                    day_seconds.getMinutesIntoHour(),
                    day_seconds.getSecondsIntoMinute(),
                }) catch " 0000 00:00:00 ";
                try buf.appendSlice(a, s2);
                try appendOffsetCompact(&buf, a, tz_offset);
            },
            'z' => {
                const jan1_day = @divFloor(dateToTimestamp(year_day.year, 1, 1, 0, 0, 0), 86400);
                try buf.writer(a).print("{d}", .{@as(i64, epoch_day.day) - jan1_day});
            },
            'W' => {
                const week = isoWeek(@intCast(epoch_day.day));
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>2}", .{week.week}) catch "01";
                try buf.appendSlice(a, s);
            },
            'w' => {
                const day_num: i64 = @intCast(epoch_day.day);
                const dow: u8 = @intCast(@mod(day_num + 4, 7)); // 0=sunday
                var tmp: [2]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{dow}) catch "0";
                try buf.appendSlice(a, s);
            },
            'L' => {
                try buf.append(a, if (isLeapYear(year_day.year)) '1' else '0');
            },
            'o' => {
                const week = isoWeek(@intCast(epoch_day.day));
                var tmp: [8]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{week.year}) catch "0000";
                try buf.appendSlice(a, s);
            },
            'X' => {
                // PHP 8.4: ISO 8601 expanded year with mandatory leading sign,
                // four-digit minimum
                var tmp: [12]u8 = undefined;
                const yr = year_day.year;
                const s = if (yr >= 0)
                    std.fmt.bufPrint(&tmp, "+{d:0>4}", .{@as(u64, @intCast(yr))}) catch "+0000"
                else
                    std.fmt.bufPrint(&tmp, "-{d:0>4}", .{@as(u64, @intCast(-yr))}) catch "-0000";
                try buf.appendSlice(a, s);
            },
            'x' => {
                // PHP 8.4: like X but the leading sign is omitted for years
                // in [0, 9999]
                var tmp: [12]u8 = undefined;
                const yr = year_day.year;
                const s = if (yr < 0)
                    std.fmt.bufPrint(&tmp, "-{d:0>4}", .{@as(u64, @intCast(-yr))}) catch "-0000"
                else if (yr > 9999)
                    std.fmt.bufPrint(&tmp, "+{d}", .{@as(u64, @intCast(yr))}) catch "+0000"
                else
                    std.fmt.bufPrint(&tmp, "{d:0>4}", .{@as(u64, @intCast(yr))}) catch "0000";
                try buf.appendSlice(a, s);
            },
            'S' => {
                const day_val = month_day.day_index + 1;
                const suffix: []const u8 = if (day_val == 11 or day_val == 12 or day_val == 13)
                    "th"
                else switch (@as(u8, @intCast(day_val % 10))) {
                    1 => "st",
                    2 => "nd",
                    3 => "rd",
                    else => "th",
                };
                try buf.appendSlice(a, suffix);
            },
            'u' => {
                var tmp: [16]u8 = undefined;
                const us_abs: u64 = @intCast(if (microseconds < 0) -microseconds else microseconds);
                const s = std.fmt.bufPrint(&tmp, "{d:0>6}", .{us_abs}) catch "000000";
                try buf.appendSlice(a, s);
            },
            'v' => {
                var tmp: [16]u8 = undefined;
                const ms_abs: u64 = @intCast(@divFloor(if (microseconds < 0) -microseconds else microseconds, 1000));
                const s = std.fmt.bufPrint(&tmp, "{d:0>3}", .{ms_abs}) catch "000";
                try buf.appendSlice(a, s);
            },
            'Z' => {
                var tmp: [12]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d}", .{tz_offset}) catch "0";
                try buf.appendSlice(a, s);
            },
            'e' => {
                try buf.appendSlice(a, tz_name);
            },
            'T' => {
                // a fixed-offset zone has no abbreviation; php spells it GMT+hhmm
                if (parseFixedOffset(tz_name)) |off| {
                    const abs: u32 = @intCast(if (off < 0) -off else off);
                    try buf.writer(a).print("GMT{c}{d:0>2}{d:0>2}", .{ @as(u8, if (off < 0) '-' else '+'), abs / 3600, (abs % 3600) / 60 });
                } else if (tzAbbrevForName(a, tz_name, timestamp)) |ab| {
                    defer a.free(ab);
                    try buf.appendSlice(a, ab);
                } else {
                    try buf.appendSlice(a, tz_name);
                }
            },
            'P' => {
                try appendOffsetColon(&buf, a, tz_offset);
            },
            'O' => {
                try appendOffsetCompact(&buf, a, tz_offset);
            },
            'I' => try buf.append(a, if (tzIsDstForName(a, tz_name, timestamp)) '1' else '0'),
            'B' => {
                // Swatch internet time: BMT = UTC+1; 1000 beats per day; .beats = (utc_secs+3600) / 86.4 % 1000
                const utc_secs: i64 = @mod(timestamp, 86400);
                const bmt = @mod(utc_secs + 3600, 86400);
                const beats: u32 = @intFromFloat(@as(f64, @floatFromInt(bmt)) / 86.4);
                var tmp: [4]u8 = undefined;
                const s = std.fmt.bufPrint(&tmp, "{d:0>3}", .{beats}) catch "000";
                try buf.appendSlice(a, s);
            },
            '\\' => {
                fi += 1;
                if (fi < format.len) try buf.append(a, format[fi]);
            },
            else => try buf.append(a, c),
        }
    }
    const result = try buf.toOwnedSlice(a);
    return NativeResult.takeString(try Value.String.adopt(a, result));
}

fn dtGetTimestamp(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    return NativeResult.scalar(.{ .int = getTimestamp(obj) });
}


fn dtModify(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len == 0 or args[0] != .string) return NativeResult.borrowed(.{ .object = obj });
    _ = try modifyDateObject(ctx, obj, args[0].string.bytes(), "DateTime::modify(): ", true);
    return NativeResult.borrowed(.{ .object = obj });
}

fn dtiModify(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len == 0 or args[0] != .string) return NativeResult.borrowed(.{ .object = obj });
    const new_obj = try cloneDateObject(ctx, obj);
    _ = try modifyDateObject(ctx, new_obj, args[0].string.bytes(), "DateTimeImmutable::modify(): ", true);
    return NativeResult.borrowed(.{ .object = new_obj });
}

// add a DateInterval to a timestamp using calendar arithmetic for y/m/d so
// month-length variation and 31st-of-month rollover behave like PHP
fn applyIntervalTz(a: Allocator, ts: i64, interval: *PhpObject, sign: i64, tz_name: []const u8) i64 {
    const y = Value.toInt(interval.get("y"));
    const m = Value.toInt(interval.get("m"));
    const d = Value.toInt(interval.get("d"));
    const h = Value.toInt(interval.get("h"));
    const i = Value.toInt(interval.get("i"));
    const s = Value.toInt(interval.get("s"));
    const invert = Value.toInt(interval.get("invert"));
    const direction: i64 = if (invert != 0) -sign else sign;

    // calendar arithmetic for y/m/d happens in the receiver's timezone so
    // crossing DST doesn't bleed into the wall-clock time
    const off_in: i64 = tzOffsetForName(a, tz_name, ts);
    const c = baseComponents(ts + off_in);
    var year: i64 = c.year + direction * y;
    var month: i64 = c.month + direction * m;
    while (month > 12) : ({
        month -= 12;
        year += 1;
    }) {}
    while (month < 1) : ({
        month += 12;
        year -= 1;
    }) {}
    const day: i64 = c.day + direction * d;

    var local_ts = dateToTimestamp(year, month, day, c.hour, c.min, c.sec);
    // convert local back to UTC
    local_ts -= tzOffsetForWallByName(a, tz_name, local_ts);
    local_ts += direction * (h * 3600 + i * 60 + s);
    return local_ts;
}

// the setters (setDate, setTime, setISODate) work on the wall-clock time in
// the object's own timezone: split the instant into local fields, change some,
// and turn the result back into an instant with the offset in effect then

fn wallFields(ctx: *NativeContext, obj: *PhpObject) DateComponents {
    const ts = getTimestamp(obj);
    return baseComponents(ts + tzOffsetForName(ctx.allocator, objTzName(obj, ctx.vm.default_tz_name), ts));
}

fn instantOfWall(ctx: *NativeContext, obj: *PhpObject, wall: i64) i64 {
    return wall - tzOffsetForWallByName(ctx.allocator, objTzName(obj, ctx.vm.default_tz_name), wall);
}

fn objTzName(obj: *PhpObject, fallback: []const u8) []const u8 {
    const v = obj.get("__timezone");
    return if (v == .string) v.string.bytes() else fallback;
}

// the setters change an object in place; DateTime's methods apply them to
// $this and DateTimeImmutable's to a clone, so both keep every other field
// (microseconds, the zone, a subclass's own properties)
const Mutation = fn (*NativeContext, *PhpObject, []const Value) RuntimeError!void;

fn mutating(comptime f: Mutation) fn (*NativeContext, []const Value) RuntimeError!NativeResult {
    return struct {
        fn call(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
            const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
            try f(ctx, obj, args);
            return NativeResult.borrowed(.{ .object = obj });
        }
    }.call;
}

fn immutable(comptime f: Mutation) fn (*NativeContext, []const Value) RuntimeError!NativeResult {
    return struct {
        fn call(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
            const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
            const copy = try cloneDateObject(ctx, obj);
            try f(ctx, copy, args);
            return NativeResult.borrowed(.{ .object = copy });
        }
    }.call;
}

fn addOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len == 0 or args[0] != .object) return;
    try obj.set(ctx.allocator, "timestamp", .{ .int = applyIntervalTz(ctx.allocator, getTimestamp(obj), args[0].object, 1, objTzName(obj, ctx.vm.default_tz_name)) });
}

fn subOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len == 0 or args[0] != .object) return;
    try obj.set(ctx.allocator, "timestamp", .{ .int = applyIntervalTz(ctx.allocator, getTimestamp(obj), args[0].object, -1, objTzName(obj, ctx.vm.default_tz_name)) });
}

fn setDateOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len < 3) return;
    const c = wallFields(ctx, obj);
    try obj.set(ctx.allocator, "timestamp", .{ .int = instantOfWall(ctx, obj, dateToTimestamp(Value.toInt(args[0]), Value.toInt(args[1]), Value.toInt(args[2]), c.hour, c.min, c.sec)) });
}

fn setTimeOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len < 2) return;
    const c = wallFields(ctx, obj);
    const sec: i64 = if (args.len >= 3) Value.toInt(args[2]) else 0;
    try obj.set(ctx.allocator, "timestamp", .{ .int = instantOfWall(ctx, obj, dateToTimestamp(c.year, c.month, c.day, Value.toInt(args[0]), Value.toInt(args[1]), sec)) });
    // the 4th argument is microseconds, and without it they reset to 0
    try obj.set(ctx.allocator, "__microseconds", .{ .int = if (args.len >= 4) Value.toInt(args[3]) else 0 });
}

fn setISODateOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len < 2) return;
    const dow = if (args.len >= 3) Value.toInt(args[2]) else 1;
    const c = wallFields(ctx, obj);
    try obj.set(ctx.allocator, "timestamp", .{ .int = instantOfWall(ctx, obj, isoWeekDateToTimestamp(Value.toInt(args[0]), Value.toInt(args[1]), dow, c.hour, c.min, c.sec)) });
}

fn setTimestampOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len < 1) return;
    try obj.set(ctx.allocator, "timestamp", .{ .int = Value.toInt(args[0]) });
    if (obj.get("__microseconds") != .null) try obj.set(ctx.allocator, "__microseconds", .{ .int = 0 });
}

fn setTimezoneOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    try obj.setCopiedString(ctx.allocator, "__timezone", extractTimezoneName(args));
}

fn setMicrosecondOn(ctx: *NativeContext, obj: *PhpObject, args: []const Value) RuntimeError!void {
    if (args.len >= 1 and args[0] == .int) try obj.set(ctx.allocator, "__microseconds", .{ .int = args[0].int });
}

const dtAdd = mutating(addOn);
const dtSub = mutating(subOn);
const dtSetDate = mutating(setDateOn);
const dtSetTime = mutating(setTimeOn);
const dtSetISODate = mutating(setISODateOn);
const dtSetTimestamp = mutating(setTimestampOn);
const dtSetTimezone = mutating(setTimezoneOn);
const dtSetMicrosecond = mutating(setMicrosecondOn);
const dtiAdd = immutable(addOn);
const dtiSub = immutable(subOn);
const dtiSetDate = immutable(setDateOn);
const dtiSetTime = immutable(setTimeOn);
const dtiSetISODate = immutable(setISODateOn);
const dtiSetTimestamp = immutable(setTimestampOn);
const dtiSetTimezone = immutable(setTimezoneOn);
const dtiSetMicrosecond = immutable(setMicrosecondOn);




fn objMicros(obj: *PhpObject) i64 {
    const v = obj.get("__microseconds");
    return if (v == .int) v.int else 0;
}

fn dtDiff(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len == 0 or args[0] != .object) return NativeResult.scalar(.null);
    const absolute = args.len >= 2 and args[1].isTruthy();
    return diffObjects(ctx, obj, args[0].object, absolute);
}

// timelib_diff over the two objects' wall-clock times
fn diffObjects(ctx: *NativeContext, obj: *PhpObject, other: *PhpObject, absolute: bool) RuntimeError!NativeResult {
    const zone1 = ParseZone.of(objTzName(obj, ctx.vm.default_tz_name), false);
    const zone2 = ParseZone.of(objTzName(other, ctx.vm.default_tz_name), false);
    const one = wallTime(ctx.allocator, getTimestamp(obj), objMicros(obj), &zone1);
    const two = wallTime(ctx.allocator, getTimestamp(other), objMicros(other), &zone2);
    const rt = dp.diff(&one, &two, parse_db);
    const interval = try ctx.createObject("DateInterval");
    try interval.set(ctx.allocator, "y", .{ .int = rt.y });
    try interval.set(ctx.allocator, "m", .{ .int = rt.m });
    try interval.set(ctx.allocator, "d", .{ .int = rt.d });
    try interval.set(ctx.allocator, "days", .{ .int = rt.days });
    try interval.set(ctx.allocator, "h", .{ .int = rt.h });
    try interval.set(ctx.allocator, "i", .{ .int = rt.i });
    try interval.set(ctx.allocator, "s", .{ .int = rt.s });
    try interval.set(ctx.allocator, "f", .{ .float = @as(f64, @floatFromInt(rt.us)) / 1_000_000.0 });
    try interval.set(ctx.allocator, "invert", .{ .int = if (rt.invert and !absolute) 1 else 0 });
    return NativeResult.borrowed(.{ .object = interval });
}




// ISO 8601 week date -> Gregorian date: jan 4 always falls in ISO week 1
// (the iso-week that contains the first Thursday of the year). The Monday
// of week 1 is jan4 minus (iso_dow_of_jan4 - 1) days, then advance by
// (week - 1) * 7 + (day - 1) days
fn isoWeekDateToTimestamp(year: i64, week: i64, day_of_week: i64, h: i64, m: i64, s: i64) i64 {
    // jan 4 unix timestamp at 00:00
    const jan4_ts = dateToTimestamp(year, 1, 4, 0, 0, 0);
    // PHP date('N') for jan 4: 1..7 (Monday..Sunday)
    const day_of_jan4 = @divFloor(jan4_ts, 86400);
    // 1970-01-01 was Thursday -> N=4. so iso_dow = ((day_of_jan4 + 3) mod 7) + 1
    var dow = @mod(day_of_jan4 + 3, 7) + 1;
    if (dow < 1) dow += 7;
    const offset_days = (week - 1) * 7 + (day_of_week - 1) - (dow - 1);
    const target_ts = jan4_ts + offset_days * 86400;
    return target_ts + h * 3600 + m * 60 + s;
}





// a float timestamp keeps its sub-second part as microseconds
fn setTimestampWithFraction(ctx: *NativeContext, obj: *PhpObject, ts: Value) !void {
    if (ts == .float) {
        const whole = @floor(ts.float);
        try obj.set(ctx.allocator, "timestamp", .{ .int = Value.dvalToLval(whole) });
        const micros: i64 = Value.dvalToLval(@round((ts.float - whole) * 1_000_000.0));
        if (micros != 0) try obj.set(ctx.allocator, "__microseconds", .{ .int = micros });
        return;
    }
    try obj.set(ctx.allocator, "timestamp", .{ .int = Value.toInt(ts) });
}

fn dtCreateFromTimestamp(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0) return NativeResult.scalar(.null);
    const obj = try ctx.createObject("DateTime");
    try setTimestampWithFraction(ctx, obj, args[0]);
    return NativeResult.borrowed(.{ .object = obj });
}

fn dtCreateFromFormat(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createFromFormatImpl(ctx, args, "DateTime");
}

fn dtCreateFromImmutable(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return convertDateObject(ctx, args, "DateTime");
}

// a DateTime from a DateTimeImmutable or the reverse: same instant, zone, and
// microseconds
fn convertDateObject(ctx: *NativeContext, args: []const Value, class_name: []const u8) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .object) return NativeResult.scalar(.null);
    const src = args[0].object;
    const obj = try ctx.createObject(class_name);
    try obj.set(ctx.allocator, "timestamp", src.get("timestamp"));
    if (src.get("__timezone") == .string) try obj.set(ctx.allocator, "__timezone", src.get("__timezone"));
    if (src.get("__microseconds") == .int) try obj.set(ctx.allocator, "__microseconds", src.get("__microseconds"));
    return NativeResult.borrowed(.{ .object = obj });
}

fn dtiCreateFromMutable(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return convertDateObject(ctx, args, "DateTimeImmutable");
}

// createFromInterface accepts either DateTime or DateTimeImmutable and produces
// the corresponding target type (called as DateTime::createFromInterface or
// DateTimeImmutable::createFromInterface)
fn dtCreateFromInterface(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return dtCreateFromImmutable(ctx, args);
}

fn dtiCreateFromInterface(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return dtiCreateFromMutable(ctx, args);
}

fn native_date_create_from_format(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createFromFormatImpl(ctx, args, "DateTime");
}

fn native_date_create_immutable_from_format(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createFromFormatImpl(ctx, args, "DateTimeImmutable");
}

fn createBareDt(ctx: *NativeContext, class_name: []const u8, args: []const Value) RuntimeError!NativeResult {
    const input: []const u8 = if (args.len >= 1 and args[0] == .string) args[0].string.bytes() else "";
    const instant = (try resolveDate(ctx, input, tzArgName(args, 1), false)) orelse return NativeResult.scalar(.{ .bool = false });
    const obj = try ctx.createObject(class_name);
    try storeInstant(ctx, obj, instant.ts, instant.us, &instant.zone);
    return NativeResult.borrowed(.{ .object = obj });
}

fn native_date_create(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createBareDt(ctx, "DateTime", args);
}

fn native_date_create_immutable(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createBareDt(ctx, "DateTimeImmutable", args);
}

fn argObj(args: []const Value) ?*PhpObject {
    if (args.len == 0 or args[0] != .object) return null;
    return args[0].object;
}

fn isImmutable(obj: *PhpObject) bool {
    return std.mem.eql(u8, obj.class_name, "DateTimeImmutable");
}

fn native_date_format(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 2 or args[1] != .string) return NativeResult.literal("");
    const ts = getTimestamp(obj);
    const tz_val = obj.get("__timezone");
    const tz_name = if (tz_val == .string) tz_val.string.bytes() else ctx.vm.default_tz_name;
    const offset = tzOffsetForName(ctx.allocator, tz_name, ts);
    return formatTimestampTz(ctx, ts, args[1].string.bytes(), offset, tz_name);
}

// procedural alias for DateInterval::createFromDateString
fn native_date_interval_create_from_date_string(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return diCreateFromDateString(ctx, args);
}

// procedural alias for DateInterval::format($interval, $format)
fn native_date_interval_format(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .object or args[1] != .string) return NativeResult.scalar(.{ .bool = false });
    const saved = ctx.vm.currentFrame().vars.get("$this");
    try ctx.vm.putFrameVar(&ctx.vm.currentFrame().vars, "$this", args[0]);
    defer if (saved) |s| (ctx.vm.putFrameVar(&ctx.vm.currentFrame().vars, "$this", s) catch {});
    return diFormat(ctx, args[1..]);
}

fn native_date_modify(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 2 or args[1] != .string) return NativeResult.borrowed(.{ .object = obj });
    const target = if (isImmutable(obj)) try cloneDateObject(ctx, obj) else obj;
    if (!try modifyDateObject(ctx, target, args[1].string.bytes(), "date_modify(): ", false)) return NativeResult.scalar(.{ .bool = false });
    return NativeResult.borrowed(.{ .object = target });
}

fn applyIntervalProc(ctx: *NativeContext, args: []const Value, sign: i64) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 2 or args[1] != .object) return NativeResult.borrowed(.{ .object = obj });
    const target = if (isImmutable(obj)) try cloneDateObject(ctx, obj) else obj;
    if (sign > 0) try addOn(ctx, target, args[1..]) else try subOn(ctx, target, args[1..]);
    return NativeResult.borrowed(.{ .object = target });
}

fn native_date_add(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return applyIntervalProc(ctx, args, 1);
}

fn native_date_sub(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return applyIntervalProc(ctx, args, -1);
}

fn native_date_diff(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .object or args[1] != .object) return NativeResult.scalar(.{ .bool = false });
    return diffObjects(ctx, args[0].object, args[1].object, args.len >= 3 and args[2].isTruthy());
}

fn native_date_timestamp_get(_: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    return NativeResult.scalar(.{ .int = getTimestamp(obj) });
}

fn native_date_timestamp_set(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 2) return NativeResult.borrowed(.{ .object = obj });
    try obj.set(ctx.allocator, "timestamp", .{ .int = Value.toInt(args[1]) });
    return NativeResult.borrowed(.{ .object = obj });
}

// a procedural function that is a method of its first argument
// (date_offset_get($d) is $d->getOffset())
fn methodAlias(comptime method: []const u8) fn (*NativeContext, []const Value) RuntimeError!NativeResult {
    return struct {
        fn call(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
            if (args.len < 1 or args[0] != .object) return NativeResult.scalar(.{ .bool = false });
            return NativeResult.share(try ctx.vm.callMethod(args[0].object, method, args[1..]));
        }
    }.call;
}

fn native_date_date_set(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 4) return NativeResult.borrowed(.{ .object = obj });
    const c = wallFields(ctx, obj);
    const new_ts = instantOfWall(ctx, obj, dateToTimestamp(Value.toInt(args[1]), Value.toInt(args[2]), Value.toInt(args[3]), c.hour, c.min, c.sec));
    try obj.set(ctx.allocator, "timestamp", .{ .int = new_ts });
    return NativeResult.borrowed(.{ .object = obj });
}

fn native_date_time_set(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = argObj(args) orelse return NativeResult.scalar(.{ .bool = false });
    if (args.len < 3) return NativeResult.borrowed(.{ .object = obj });
    const c = wallFields(ctx, obj);
    const sec: i64 = if (args.len >= 4) Value.toInt(args[3]) else 0;
    const new_ts = instantOfWall(ctx, obj, dateToTimestamp(c.year, c.month, c.day, Value.toInt(args[1]), Value.toInt(args[2]), sec));
    try obj.set(ctx.allocator, "timestamp", .{ .int = new_ts });
    return NativeResult.borrowed(.{ .object = obj });
}

fn dtiCreateFromFormat(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    return createFromFormatImpl(ctx, args, "DateTimeImmutable");
}

fn createFromFormatImpl(ctx: *NativeContext, args: []const Value, default_class: []const u8) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .string or args[1] != .string) return NativeResult.scalar(.{ .bool = false });
    const format = args[0].string.bytes();
    const datetime = args[1].string.bytes();

    // late static binding: if called from a subclass (e.g. Carbon), create that class
    const class_name = blk: {
        var fi: usize = ctx.vm.frame_count;
        while (fi > 0) {
            fi -= 1;
            if (ctx.vm.frames[fi].called_class) |cc| {
                if (ctx.vm.classes.contains(cc)) break :blk cc;
            }
        }
        break :blk default_class;
    };

    const res = parseDateTimeFormat(format, datetime, std.time.timestamp()) orelse {
        var rec = vm_mod.DtLastErrors{ .set = true };
        rec.addError(0, "A four digit year could not be found");
        rec.addError(datetime.len, "Not enough data available to satisfy format");
        rec.error_count = 3;
        ctx.vm.last_dt = rec;
        return NativeResult.scalar(.{ .bool = false });
    };
    ctx.vm.last_dt = .{};
    const obj = try ctx.createObject(class_name);

    // optional 3rd arg: DateTimeZone. PHP applies it as the default zone when
    // the format string didn't already specify one. without this, an explicit
    // tz arg to createFromFormat was ignored and DST/regional offsets were
    // wrong (everything treated as UTC)
    var final_ts = res.ts;
    var timezone: ?Value.String = null;
    defer if (timezone) |name| name.release();
    if (res.tz_offset_seconds) |off| {
        const sign: u8 = if (off < 0) '-' else '+';
        const abs: u32 = @intCast(if (off < 0) -off else off);
        const hh = abs / 3600;
        const mm = (abs % 3600) / 60;
        const n = try std.fmt.allocPrint(ctx.allocator, "{c}{d:0>2}:{d:0>2}", .{ sign, hh, mm });
        timezone = try Value.String.adopt(ctx.allocator, n);
    } else if (args.len >= 3 and args[2] == .object and std.mem.eql(u8, args[2].object.class_name, "DateTimeZone")) {
        const nv = args[2].object.get("timezone");
        if (nv == .string) {
            nv.string.retain();
            timezone = nv.string;
            // adjust timestamp: parseDateTimeFormat returned a UTC-interpreted
            // ts but the user meant the wall clock in the explicit zone.
            // subtract the zone's offset at that moment to get the real UTC ts
            const off: i64 = if (lookupTimezone(nv.string.bytes())) |tz| tzOffsetAt(tz, res.ts) else 0;
            final_ts = res.ts - off;
        }
    }
    try obj.set(ctx.allocator, "timestamp", .{ .int = final_ts });
    if (timezone) |name| try obj.set(ctx.allocator, "__timezone", .{ .string = name });
    if (res.microseconds != 0) try obj.set(ctx.allocator, "__microseconds", .{ .int = res.microseconds });
    return NativeResult.borrowed(.{ .object = obj });
}

const ParsedDateTime = struct { ts: i64, tz_offset_seconds: ?i32, microseconds: i64 = 0 };

// PHP createFromFormat parser. Supports the common specifiers: Y y m n M F d j D l
// H G h g i s U a A e T P O Z, plus literal escape (\X) and reset markers (! and |).
// Returns null on parse failure (caller maps to PHP `false`).
fn parseDateTimeFormat(format: []const u8, datetime: []const u8, now: i64) ?ParsedDateTime {
    const ncomps = baseComponents(now);
    var year: i64 = ncomps.year;
    var month: i64 = ncomps.month;
    var day: i64 = ncomps.day;
    var hour: i64 = ncomps.hour;
    var min: i64 = ncomps.min;
    var sec: i64 = ncomps.sec;
    var u_ts: ?i64 = null;
    var tz_offset: i64 = 0;
    var tz_parsed: bool = false;
    var is_pm: ?bool = null;
    var hour_is_12: bool = false;
    var microseconds: i64 = 0;

    // PHP `!` reset semantics: track which fields have been parsed so far so `|` can reset the rest
    var parsed_year = false;
    var parsed_month = false;
    var parsed_day = false;
    var parsed_hour = false;
    var parsed_min = false;
    var parsed_sec = false;

    var fi: usize = 0;
    var di: usize = 0;
    while (fi < format.len) : (fi += 1) {
        const c = format[fi];
        switch (c) {
            '!' => {
                year = 1970;
                month = 1;
                day = 1;
                hour = 0;
                min = 0;
                sec = 0;
                parsed_year = true;
                parsed_month = true;
                parsed_day = true;
                parsed_hour = true;
                parsed_min = true;
                parsed_sec = true;
            },
            '|' => {
                if (!parsed_year) year = 1970;
                if (!parsed_month) month = 1;
                if (!parsed_day) day = 1;
                if (!parsed_hour) hour = 0;
                if (!parsed_min) min = 0;
                if (!parsed_sec) sec = 0;
            },
            '\\' => {
                fi += 1;
                if (fi >= format.len) return null;
                if (di >= datetime.len or datetime[di] != format[fi]) return null;
                di += 1;
            },
            'Y' => {
                if (di + 4 > datetime.len) return null;
                year = std.fmt.parseInt(i64, datetime[di .. di + 4], 10) catch return null;
                di += 4;
                parsed_year = true;
            },
            'y' => {
                if (di + 2 > datetime.len) return null;
                const yy = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                year = if (yy < 70) 2000 + yy else 1900 + yy;
                di += 2;
                parsed_year = true;
            },
            'm' => {
                if (di + 2 > datetime.len) return null;
                month = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_month = true;
            },
            'n' => {
                const took = takeDigits(datetime, di, 1, 2) orelse return null;
                month = took.value;
                di = took.next;
                parsed_month = true;
            },
            'd' => {
                if (di + 2 > datetime.len) return null;
                day = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_day = true;
            },
            'j' => {
                const took = takeDigits(datetime, di, 1, 2) orelse return null;
                day = took.value;
                di = took.next;
                parsed_day = true;
            },
            'H' => {
                if (di + 2 > datetime.len) return null;
                hour = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_hour = true;
            },
            'G' => {
                const took = takeDigits(datetime, di, 1, 2) orelse return null;
                hour = took.value;
                di = took.next;
                parsed_hour = true;
            },
            'h' => {
                if (di + 2 > datetime.len) return null;
                hour = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_hour = true;
                hour_is_12 = true;
            },
            'g' => {
                const took = takeDigits(datetime, di, 1, 2) orelse return null;
                hour = took.value;
                di = took.next;
                parsed_hour = true;
                hour_is_12 = true;
            },
            'i' => {
                if (di + 2 > datetime.len) return null;
                min = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_min = true;
            },
            's' => {
                if (di + 2 > datetime.len) return null;
                sec = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                parsed_sec = true;
            },
            'U' => {
                const start = di;
                if (di < datetime.len and datetime[di] == '-') di += 1;
                while (di < datetime.len and datetime[di] >= '0' and datetime[di] <= '9') : (di += 1) {}
                if (di == start or (di == start + 1 and datetime[start] == '-')) return null;
                u_ts = std.fmt.parseInt(i64, datetime[start..di], 10) catch return null;
            },
            'u' => {
                const start = di;
                while (di < datetime.len and di - start < 6 and datetime[di] >= '0' and datetime[di] <= '9') : (di += 1) {}
                if (di == start) return null;
                var parsed_u = std.fmt.parseInt(i64, datetime[start..di], 10) catch 0;
                // pad to 6-digit microsecond resolution (PHP's u is microseconds)
                var pad = 6 - (di - start);
                while (pad > 0) : (pad -= 1) parsed_u *= 10;
                microseconds = parsed_u;
            },
            'v' => {
                const start = di;
                while (di < datetime.len and di - start < 3 and datetime[di] >= '0' and datetime[di] <= '9') : (di += 1) {}
                if (di == start) return null;
                var parsed_v = std.fmt.parseInt(i64, datetime[start..di], 10) catch 0;
                // v is milliseconds; scale to microseconds
                var pad = 3 - (di - start);
                while (pad > 0) : (pad -= 1) parsed_v *= 10;
                microseconds = parsed_v * 1000;
            },
            'a', 'A' => {
                if (di + 2 > datetime.len) return null;
                const seg = datetime[di .. di + 2];
                if (eqlLower(seg, "am")) is_pm = false else if (eqlLower(seg, "pm")) is_pm = true else return null;
                di += 2;
            },
            'M' => {
                const m = parseShortMonth(datetime[di..]) orelse return null;
                month = m;
                di += 3;
                parsed_month = true;
            },
            'F' => {
                const len = monthNameLen(datetime[di..]);
                if (len == 0) return null;
                month = parseMonthName(datetime[di..]) orelse return null;
                di += len;
                parsed_month = true;
            },
            'D' => {
                if (di + 3 > datetime.len) return null;
                di += 3;
            },
            'l' => {
                const len = weekdayNameLen(datetime[di..]) orelse return null;
                di += len;
            },
            'e', 'T' => {
                // timezone name: consume identifier-ish chars
                const start = di;
                while (di < datetime.len and (isAlpha(datetime[di]) or datetime[di] == '/' or datetime[di] == '_' or datetime[di] == '+' or datetime[di] == '-' or (datetime[di] >= '0' and datetime[di] <= '9'))) : (di += 1) {}
                if (di == start) return null;
            },
            'O', 'P' => {
                // +0200 or +02:00
                if (di >= datetime.len) return null;
                const sign: i64 = if (datetime[di] == '+') 1 else if (datetime[di] == '-') -1 else return null;
                di += 1;
                if (di + 2 > datetime.len) return null;
                const hh = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch return null;
                di += 2;
                var mm: i64 = 0;
                if (di < datetime.len and datetime[di] == ':') di += 1;
                if (di + 2 <= datetime.len and datetime[di] >= '0' and datetime[di] <= '9' and datetime[di + 1] >= '0' and datetime[di + 1] <= '9') {
                    mm = std.fmt.parseInt(i64, datetime[di .. di + 2], 10) catch 0;
                    di += 2;
                }
                tz_offset = sign * (hh * 3600 + mm * 60);
                tz_parsed = true;
            },
            'Z' => {
                const start = di;
                if (di < datetime.len and (datetime[di] == '+' or datetime[di] == '-')) di += 1;
                while (di < datetime.len and datetime[di] >= '0' and datetime[di] <= '9') : (di += 1) {}
                if (di == start or (di == start + 1 and !isDigit(datetime[start]))) return null;
                tz_offset = std.fmt.parseInt(i64, datetime[start..di], 10) catch return null;
                tz_parsed = true;
            },
            ' ' => {
                while (di < datetime.len and datetime[di] == ' ') di += 1;
            },
            else => {
                if (di >= datetime.len or datetime[di] != c) return null;
                di += 1;
            },
        }
    }

    const parsed_off: ?i32 = if (tz_parsed) @intCast(tz_offset) else null;

    if (u_ts) |ts| return .{ .ts = ts - tz_offset, .tz_offset_seconds = parsed_off, .microseconds = microseconds };

    if (hour_is_12) {
        if (is_pm) |pm| {
            if (pm and hour < 12) hour += 12 else if (!pm and hour == 12) hour = 0;
        }
    }

    // PHP zeroes unset time components when ANY time component is parsed.
    // Without this, an "Y-m-d H:i" format would leave seconds at current time.
    const any_time_parsed = parsed_hour or parsed_min or parsed_sec;
    if (any_time_parsed) {
        if (!parsed_hour) hour = 0;
        if (!parsed_min) min = 0;
        if (!parsed_sec) sec = 0;
    }

    return .{ .ts = dateToTimestamp(year, month, day, hour, min, sec) - tz_offset, .tz_offset_seconds = parsed_off, .microseconds = microseconds };
}

fn isDigit(c: u8) bool {
    return c >= '0' and c <= '9';
}
fn isAlpha(c: u8) bool {
    return (c >= 'a' and c <= 'z') or (c >= 'A' and c <= 'Z');
}

fn takeDigits(s: []const u8, start: usize, min_n: usize, max_n: usize) ?struct { value: i64, next: usize } {
    var i = start;
    while (i < s.len and i - start < max_n and isDigit(s[i])) : (i += 1) {}
    const got = i - start;
    if (got < min_n) return null;
    const v = std.fmt.parseInt(i64, s[start..i], 10) catch return null;
    return .{ .value = v, .next = i };
}

fn parseShortMonth(s: []const u8) ?i64 {
    const months = [_][]const u8{ "jan", "feb", "mar", "apr", "may", "jun", "jul", "aug", "sep", "oct", "nov", "dec" };
    if (s.len < 3) return null;
    for (months, 1..) |name, i| {
        if (eqlLower(s[0..3], name)) return @intCast(i);
    }
    return null;
}

fn weekdayNameLen(s: []const u8) ?usize {
    const days = [_][]const u8{ "sunday", "monday", "tuesday", "wednesday", "thursday", "friday", "saturday" };
    for (days) |name| {
        if (s.len >= name.len and eqlLower(s[0..name.len], name)) return name.len;
    }
    return null;
}

fn dtiCreateFromTimestamp(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0) return NativeResult.scalar(.null);
    const obj = try ctx.createObject("DateTimeImmutable");
    try setTimestampWithFraction(ctx, obj, args[0]);
    return NativeResult.borrowed(.{ .object = obj });
}

fn dtGetMicrosecond(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.{ .int = 0 });
    const v = obj.get("__microseconds");
    if (v == .int) return NativeResult.scalar(v);
    return NativeResult.scalar(.{ .int = 0 });
}


fn dtGetTimezone(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    const tz_val = obj.get("__timezone");
    const tz_obj = try ctx.createObject("DateTimeZone");
    try tz_obj.set(ctx.allocator, "timezone", if (tz_val == .string) tz_val else .{ .string = Value.String.borrowed("UTC") });
    return NativeResult.borrowed(.{ .object = tz_obj });
}

fn dtGetOffset(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.{ .int = 0 });
    const tz_val = obj.get("__timezone");
    const tz_name = if (tz_val == .string) tz_val.string.bytes() else "UTC";
    const ts = getTimestamp(obj);
    return NativeResult.scalar(.{ .int = @intCast(tzOffsetForName(ctx.allocator, tz_name, ts)) });
}



fn extractTimezoneName(args: []const Value) []const u8 {
    if (args.len == 0) return "UTC";
    if (args[0] == .string) return args[0].string.bytes();
    if (args[0] == .object) {
        const tz_val = args[0].object.get("timezone");
        if (tz_val == .string) return tz_val.string.bytes();
    }
    return "UTC";
}

fn dtzConstruct(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len >= 1 and args[0] == .string) {
        const zone = (try timezoneInitialize(ctx, args[0].string.bytes(), "DateTimeZone::__construct(): ", true)) orelse return error.RuntimeError;
        try obj.setCopiedString(ctx.allocator, "timezone", zone.name());
    }
    return NativeResult.scalar(.null);
}

// timezone_initialize: a zone string read the way the date parser reads one,
// so "cest" is the abbreviation CEST, "+0530" the offset +05:30, and anything
// left over makes it a bad timezone
fn timezoneInitialize(ctx: *NativeContext, name: []const u8, prefix: []const u8, throws: bool) RuntimeError!?ParseZone {
    const zp = dp.parseZoneString(name, parse_db);
    var what: ?[]const u8 = null;
    if (zp.time.z >= 100 * 3600 or zp.time.z <= -100 * 3600) {
        what = "Timezone offset is out of range";
    } else if (zp.not_found or zp.rest != 0 or std.mem.indexOfScalar(u8, name, 0) != null) {
        what = "Unknown or bad timezone";
    }
    if (what) |w| {
        const msg = std.fmt.allocPrint(ctx.allocator, "{s}{s} ({s})", .{ prefix, w, name }) catch return error.OutOfMemory;
        defer ctx.allocator.free(msg);
        if (throws) {
            try ctx.vm.setPendingException("DateInvalidTimeZoneException", msg);
        } else {
            try ctx.vm.emitWarning(msg);
        }
        return null;
    }
    return ParseZone.ofTime(&zp.time);
}

fn dtzListIdentifiers(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    // full IANA zone list (419 entries) matching PHP 8.4's tzdb snapshot. used
    // for membership checks in user code that whitelists allowed zones; the
    // names themselves are also valid `DateTimeZone` constructor inputs
    const ids = [_][]const u8{
        "Africa/Abidjan",                 "Africa/Accra",                "Africa/Addis_Ababa",
        "Africa/Algiers",                 "Africa/Asmara",               "Africa/Bamako",
        "Africa/Bangui",                  "Africa/Banjul",               "Africa/Bissau",
        "Africa/Blantyre",                "Africa/Brazzaville",          "Africa/Bujumbura",
        "Africa/Cairo",                   "Africa/Casablanca",           "Africa/Ceuta",
        "Africa/Conakry",                 "Africa/Dakar",                "Africa/Dar_es_Salaam",
        "Africa/Djibouti",                "Africa/Douala",               "Africa/El_Aaiun",
        "Africa/Freetown",                "Africa/Gaborone",             "Africa/Harare",
        "Africa/Johannesburg",            "Africa/Juba",                 "Africa/Kampala",
        "Africa/Khartoum",                "Africa/Kigali",               "Africa/Kinshasa",
        "Africa/Lagos",                   "Africa/Libreville",           "Africa/Lome",
        "Africa/Luanda",                  "Africa/Lubumbashi",           "Africa/Lusaka",
        "Africa/Malabo",                  "Africa/Maputo",               "Africa/Maseru",
        "Africa/Mbabane",                 "Africa/Mogadishu",            "Africa/Monrovia",
        "Africa/Nairobi",                 "Africa/Ndjamena",             "Africa/Niamey",
        "Africa/Nouakchott",              "Africa/Ouagadougou",          "Africa/Porto-Novo",
        "Africa/Sao_Tome",                "Africa/Tripoli",              "Africa/Tunis",
        "Africa/Windhoek",                "America/Adak",                "America/Anchorage",
        "America/Anguilla",               "America/Antigua",             "America/Araguaina",
        "America/Argentina/Buenos_Aires", "America/Argentina/Catamarca", "America/Argentina/Cordoba",
        "America/Argentina/Jujuy",        "America/Argentina/La_Rioja",  "America/Argentina/Mendoza",
        "America/Argentina/Rio_Gallegos", "America/Argentina/Salta",     "America/Argentina/San_Juan",
        "America/Argentina/San_Luis",     "America/Argentina/Tucuman",   "America/Argentina/Ushuaia",
        "America/Aruba",                  "America/Asuncion",            "America/Atikokan",
        "America/Bahia",                  "America/Bahia_Banderas",      "America/Barbados",
        "America/Belem",                  "America/Belize",              "America/Blanc-Sablon",
        "America/Boa_Vista",              "America/Bogota",              "America/Boise",
        "America/Cambridge_Bay",          "America/Campo_Grande",        "America/Cancun",
        "America/Caracas",                "America/Cayenne",             "America/Cayman",
        "America/Chicago",                "America/Chihuahua",           "America/Ciudad_Juarez",
        "America/Costa_Rica",             "America/Coyhaique",           "America/Creston",
        "America/Cuiaba",                 "America/Curacao",             "America/Danmarkshavn",
        "America/Dawson",                 "America/Dawson_Creek",        "America/Denver",
        "America/Detroit",                "America/Dominica",            "America/Edmonton",
        "America/Eirunepe",               "America/El_Salvador",         "America/Fort_Nelson",
        "America/Fortaleza",              "America/Glace_Bay",           "America/Goose_Bay",
        "America/Grand_Turk",             "America/Grenada",             "America/Guadeloupe",
        "America/Guatemala",              "America/Guayaquil",           "America/Guyana",
        "America/Halifax",                "America/Havana",              "America/Hermosillo",
        "America/Indiana/Indianapolis",   "America/Indiana/Knox",        "America/Indiana/Marengo",
        "America/Indiana/Petersburg",     "America/Indiana/Tell_City",   "America/Indiana/Vevay",
        "America/Indiana/Vincennes",      "America/Indiana/Winamac",     "America/Inuvik",
        "America/Iqaluit",                "America/Jamaica",             "America/Juneau",
        "America/Kentucky/Louisville",    "America/Kentucky/Monticello", "America/Kralendijk",
        "America/La_Paz",                 "America/Lima",                "America/Los_Angeles",
        "America/Lower_Princes",          "America/Maceio",              "America/Managua",
        "America/Manaus",                 "America/Marigot",             "America/Martinique",
        "America/Matamoros",              "America/Mazatlan",            "America/Menominee",
        "America/Merida",                 "America/Metlakatla",          "America/Mexico_City",
        "America/Miquelon",               "America/Moncton",             "America/Monterrey",
        "America/Montevideo",             "America/Montserrat",          "America/Nassau",
        "America/New_York",               "America/Nome",                "America/Noronha",
        "America/North_Dakota/Beulah",    "America/North_Dakota/Center", "America/North_Dakota/New_Salem",
        "America/Nuuk",                   "America/Ojinaga",             "America/Panama",
        "America/Paramaribo",             "America/Phoenix",             "America/Port-au-Prince",
        "America/Port_of_Spain",          "America/Porto_Velho",         "America/Puerto_Rico",
        "America/Punta_Arenas",           "America/Rankin_Inlet",        "America/Recife",
        "America/Regina",                 "America/Resolute",            "America/Rio_Branco",
        "America/Santarem",               "America/Santiago",            "America/Santo_Domingo",
        "America/Sao_Paulo",              "America/Scoresbysund",        "America/Sitka",
        "America/St_Barthelemy",          "America/St_Johns",            "America/St_Kitts",
        "America/St_Lucia",               "America/St_Thomas",           "America/St_Vincent",
        "America/Swift_Current",          "America/Tegucigalpa",         "America/Thule",
        "America/Tijuana",                "America/Toronto",             "America/Tortola",
        "America/Vancouver",              "America/Whitehorse",          "America/Winnipeg",
        "America/Yakutat",                "Antarctica/Casey",            "Antarctica/Davis",
        "Antarctica/DumontDUrville",      "Antarctica/Macquarie",        "Antarctica/Mawson",
        "Antarctica/McMurdo",             "Antarctica/Palmer",           "Antarctica/Rothera",
        "Antarctica/Syowa",               "Antarctica/Troll",            "Antarctica/Vostok",
        "Arctic/Longyearbyen",            "Asia/Aden",                   "Asia/Almaty",
        "Asia/Amman",                     "Asia/Anadyr",                 "Asia/Aqtau",
        "Asia/Aqtobe",                    "Asia/Ashgabat",               "Asia/Atyrau",
        "Asia/Baghdad",                   "Asia/Bahrain",                "Asia/Baku",
        "Asia/Bangkok",                   "Asia/Barnaul",                "Asia/Beirut",
        "Asia/Bishkek",                   "Asia/Brunei",                 "Asia/Chita",
        "Asia/Colombo",                   "Asia/Damascus",               "Asia/Dhaka",
        "Asia/Dili",                      "Asia/Dubai",                  "Asia/Dushanbe",
        "Asia/Famagusta",                 "Asia/Gaza",                   "Asia/Hebron",
        "Asia/Ho_Chi_Minh",               "Asia/Hong_Kong",              "Asia/Hovd",
        "Asia/Irkutsk",                   "Asia/Jakarta",                "Asia/Jayapura",
        "Asia/Jerusalem",                 "Asia/Kabul",                  "Asia/Kamchatka",
        "Asia/Karachi",                   "Asia/Kathmandu",              "Asia/Khandyga",
        "Asia/Kolkata",                   "Asia/Krasnoyarsk",            "Asia/Kuala_Lumpur",
        "Asia/Kuching",                   "Asia/Kuwait",                 "Asia/Macau",
        "Asia/Magadan",                   "Asia/Makassar",               "Asia/Manila",
        "Asia/Muscat",                    "Asia/Nicosia",                "Asia/Novokuznetsk",
        "Asia/Novosibirsk",               "Asia/Omsk",                   "Asia/Oral",
        "Asia/Phnom_Penh",                "Asia/Pontianak",              "Asia/Pyongyang",
        "Asia/Qatar",                     "Asia/Qostanay",               "Asia/Qyzylorda",
        "Asia/Riyadh",                    "Asia/Sakhalin",               "Asia/Samarkand",
        "Asia/Seoul",                     "Asia/Shanghai",               "Asia/Singapore",
        "Asia/Srednekolymsk",             "Asia/Taipei",                 "Asia/Tashkent",
        "Asia/Tbilisi",                   "Asia/Tehran",                 "Asia/Thimphu",
        "Asia/Tokyo",                     "Asia/Tomsk",                  "Asia/Ulaanbaatar",
        "Asia/Urumqi",                    "Asia/Ust-Nera",               "Asia/Vientiane",
        "Asia/Vladivostok",               "Asia/Yakutsk",                "Asia/Yangon",
        "Asia/Yekaterinburg",             "Asia/Yerevan",                "Atlantic/Azores",
        "Atlantic/Bermuda",               "Atlantic/Canary",             "Atlantic/Cape_Verde",
        "Atlantic/Faroe",                 "Atlantic/Madeira",            "Atlantic/Reykjavik",
        "Atlantic/South_Georgia",         "Atlantic/St_Helena",          "Atlantic/Stanley",
        "Australia/Adelaide",             "Australia/Brisbane",          "Australia/Broken_Hill",
        "Australia/Darwin",               "Australia/Eucla",             "Australia/Hobart",
        "Australia/Lindeman",             "Australia/Lord_Howe",         "Australia/Melbourne",
        "Australia/Perth",                "Australia/Sydney",            "Europe/Amsterdam",
        "Europe/Andorra",                 "Europe/Astrakhan",            "Europe/Athens",
        "Europe/Belgrade",                "Europe/Berlin",               "Europe/Bratislava",
        "Europe/Brussels",                "Europe/Bucharest",            "Europe/Budapest",
        "Europe/Busingen",                "Europe/Chisinau",             "Europe/Copenhagen",
        "Europe/Dublin",                  "Europe/Gibraltar",            "Europe/Guernsey",
        "Europe/Helsinki",                "Europe/Isle_of_Man",          "Europe/Istanbul",
        "Europe/Jersey",                  "Europe/Kaliningrad",          "Europe/Kirov",
        "Europe/Kyiv",                    "Europe/Lisbon",               "Europe/Ljubljana",
        "Europe/London",                  "Europe/Luxembourg",           "Europe/Madrid",
        "Europe/Malta",                   "Europe/Mariehamn",            "Europe/Minsk",
        "Europe/Monaco",                  "Europe/Moscow",               "Europe/Oslo",
        "Europe/Paris",                   "Europe/Podgorica",            "Europe/Prague",
        "Europe/Riga",                    "Europe/Rome",                 "Europe/Samara",
        "Europe/San_Marino",              "Europe/Sarajevo",             "Europe/Saratov",
        "Europe/Simferopol",              "Europe/Skopje",               "Europe/Sofia",
        "Europe/Stockholm",               "Europe/Tallinn",              "Europe/Tirane",
        "Europe/Ulyanovsk",               "Europe/Vaduz",                "Europe/Vatican",
        "Europe/Vienna",                  "Europe/Vilnius",              "Europe/Volgograd",
        "Europe/Warsaw",                  "Europe/Zagreb",               "Europe/Zurich",
        "Indian/Antananarivo",            "Indian/Chagos",               "Indian/Christmas",
        "Indian/Cocos",                   "Indian/Comoro",               "Indian/Kerguelen",
        "Indian/Mahe",                    "Indian/Maldives",             "Indian/Mauritius",
        "Indian/Mayotte",                 "Indian/Reunion",              "Pacific/Apia",
        "Pacific/Auckland",               "Pacific/Bougainville",        "Pacific/Chatham",
        "Pacific/Chuuk",                  "Pacific/Easter",              "Pacific/Efate",
        "Pacific/Fakaofo",                "Pacific/Fiji",                "Pacific/Funafuti",
        "Pacific/Galapagos",              "Pacific/Gambier",             "Pacific/Guadalcanal",
        "Pacific/Guam",                   "Pacific/Honolulu",            "Pacific/Kanton",
        "Pacific/Kiritimati",             "Pacific/Kosrae",              "Pacific/Kwajalein",
        "Pacific/Majuro",                 "Pacific/Marquesas",           "Pacific/Midway",
        "Pacific/Nauru",                  "Pacific/Niue",                "Pacific/Norfolk",
        "Pacific/Noumea",                 "Pacific/Pago_Pago",           "Pacific/Palau",
        "Pacific/Pitcairn",               "Pacific/Pohnpei",             "Pacific/Port_Moresby",
        "Pacific/Rarotonga",              "Pacific/Saipan",              "Pacific/Tahiti",
        "Pacific/Tarawa",                 "Pacific/Tongatapu",           "Pacific/Wake",
        "Pacific/Wallis",                 "UTC",
    };
    var arr = try ctx.createArray();
    for (ids) |id| try arr.append(ctx.allocator, .{ .string = Value.String.borrowed(id) });
    return NativeResult.borrowed(.{ .array = arr });
}

fn dtzListAbbreviations(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const arr = try ctx.createArray();
    // PHP keys are lowercase abbreviation strings; each value is a list of
    // {dst, offset, timezone_id} entries. iterate our tz_table and emit one
    // entry per (zone, dst-side) where the abbreviation is non-empty.
    for (tz_table) |z| {
        // build the canonical PHP timezone id (e.g. "America/New_York" from
        // the lowercased table name). reusing the tz_table entry's name as
        // lowercase is fine — PHP accepts case-insensitive zone names
        var key_buf: [16]u8 = undefined;
        // emit std side
        if (z.std_abbrev.len > 0 and z.std_abbrev.len < key_buf.len) {
            for (z.std_abbrev, 0..) |c, i| key_buf[i] = std.ascii.toLower(c);
            const key = try Value.String.create(ctx.allocator, key_buf[0..z.std_abbrev.len]);
            defer key.release();
            const canonical = try canonicalizeZoneName(ctx, z.name);
            defer canonical.release();
            try appendAbbrevEntry(ctx, arr, key, false, z.std_offset, canonical);
        }
        // emit dst side only when distinct
        if (z.dst_rule != .none and z.dst_abbrev.len > 0 and !std.mem.eql(u8, z.std_abbrev, z.dst_abbrev) and z.dst_abbrev.len < key_buf.len) {
            for (z.dst_abbrev, 0..) |c, i| key_buf[i] = std.ascii.toLower(c);
            const key = try Value.String.create(ctx.allocator, key_buf[0..z.dst_abbrev.len]);
            defer key.release();
            const canonical = try canonicalizeZoneName(ctx, z.name);
            defer canonical.release();
            try appendAbbrevEntry(ctx, arr, key, true, z.dst_offset, canonical);
        }
    }
    // the single-letter military timezones (RFC 822 / NATO phonetic). these are
    // fixed by definition, not derived from the tz database, so they're stable
    // across tzdb versions. PHP lists each with a null timezone_id. 'j' (local)
    // is intentionally absent. the multi-letter historical abbreviations PHP
    // also returns are intentionally NOT fully reproduced here: that set is
    // coupled to PHP's bundled timezonedb version (per-abbreviation zone lists
    // shift across releases), so chasing byte-for-byte parity would be brittle
    // for a near-unused function - treated as implementation-defined
    const military = [_]struct { c: u8, off: i32 }{
        .{ .c = 'a', .off = 3600 },   .{ .c = 'b', .off = 7200 },   .{ .c = 'c', .off = 10800 },
        .{ .c = 'd', .off = 14400 },  .{ .c = 'e', .off = 18000 },  .{ .c = 'f', .off = 21600 },
        .{ .c = 'g', .off = 25200 },  .{ .c = 'h', .off = 28800 },  .{ .c = 'i', .off = 32400 },
        .{ .c = 'k', .off = 36000 },  .{ .c = 'l', .off = 39600 },  .{ .c = 'm', .off = 43200 },
        .{ .c = 'n', .off = -3600 },  .{ .c = 'o', .off = -7200 },  .{ .c = 'p', .off = -10800 },
        .{ .c = 'q', .off = -14400 }, .{ .c = 'r', .off = -18000 }, .{ .c = 's', .off = -21600 },
        .{ .c = 't', .off = -25200 }, .{ .c = 'u', .off = -28800 }, .{ .c = 'v', .off = -32400 },
        .{ .c = 'w', .off = -36000 }, .{ .c = 'x', .off = -39600 }, .{ .c = 'y', .off = -43200 },
        .{ .c = 'z', .off = 0 },
    };
    for (military) |m| {
        if (arr.get(.{ .string = Value.String.borrowed(&[_]u8{m.c}) }) != .null) continue;
        const key = try Value.String.create(ctx.allocator, &[_]u8{m.c});
        defer key.release();
        const list = try ctx.createArray();
        const entry = try ctx.createArray();
        try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("dst") }, .{ .bool = false });
        try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("offset") }, .{ .int = @as(i64, m.off) });
        try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("timezone_id") }, .null);
        try list.append(ctx.allocator, .{ .array = entry });
        try arr.set(ctx.allocator, .{ .string = key }, .{ .array = list });
    }
    return NativeResult.borrowed(.{ .array = arr });
}

fn canonicalizeZoneName(ctx: *NativeContext, lower_name: []const u8) !Value.String {
    // turn "america/new_york" into "America/New_York"
    const out = try ctx.allocator.dupe(u8, lower_name);
    var capitalize_next = true;
    for (out, 0..) |c, i| {
        if (c == '/' or c == '_') {
            capitalize_next = true;
        } else if (capitalize_next) {
            out[i] = std.ascii.toUpper(c);
            capitalize_next = false;
        }
    }
    return Value.String.adopt(ctx.allocator, out);
}

fn appendAbbrevEntry(ctx: *NativeContext, outer: *@import("../runtime/value.zig").PhpArray, key: Value.String, dst: bool, offset: i32, tz_id: Value.String) !void {
    const PA = @import("../runtime/value.zig").PhpArray;
    var list: *PA = undefined;
    const existing = outer.get(.{ .string = key });
    if (existing == .array) {
        list = existing.array;
    } else {
        list = try ctx.createArray();
        try outer.set(ctx.allocator, .{ .string = key }, .{ .array = list });
    }
    const entry = try ctx.createArray();
    try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("dst") }, .{ .bool = dst });
    try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("offset") }, .{ .int = @as(i64, offset) });
    try entry.set(ctx.allocator, .{ .string = Value.String.borrowed("timezone_id") }, .{ .string = tz_id });
    try list.append(ctx.allocator, .{ .array = entry });
}

fn dtzGetName(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.literal("UTC");
    const tz_val = obj.get("timezone");
    return if (tz_val == .string) NativeResult.shareString(tz_val.string) else NativeResult.literal("UTC");
}

fn dtzGetOffset(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.{ .int = 0 });
    const tz_val = obj.get("timezone");
    const tz_name = if (tz_val == .string) tz_val.string.bytes() else "UTC";

    // getOffset takes a DateTime argument for DST calculation
    var ref_ts: i64 = std.time.timestamp();
    if (args.len >= 1 and args[0] == .object) {
        ref_ts = getTimestamp(args[0].object);
    }

    return NativeResult.scalar(.{ .int = @intCast(tzOffsetForName(ctx.allocator, tz_name, ref_ts)) });
}

// PHP's getLocation returns {country_code, latitude, longitude, comments}
// for named tz, and false for offset-only zones. zphp doesn't ship the IANA
// tzdata, so named zones return placeholder data with the right structure
fn dtzGetLocation(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.{ .bool = false });
    const tz_val = obj.get("timezone");
    const tz_name = if (tz_val == .string) tz_val.string.bytes() else "UTC";
    // offset-only zones report false
    if (tz_name.len > 0 and (tz_name[0] == '+' or tz_name[0] == '-')) return NativeResult.scalar(.{ .bool = false });
    const arr = try ctx.createArray();
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("country_code") }, .{ .string = Value.String.borrowed("??") });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("latitude") }, .{ .float = 0.0 });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("longitude") }, .{ .float = 0.0 });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("comments") }, .{ .string = Value.String.borrowed("") });
    return NativeResult.borrowed(.{ .array = arr });
}

// stub that returns an empty list - we don't track historical transitions
fn dtzGetTransitions(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    return NativeResult.borrowed(.{ .array = try ctx.createArray() });
}

// standalone PHP functions: date(), mktime(), strtotime(), time(), microtime()

fn appendOffsetColon(buf: *std.ArrayListUnmanaged(u8), a: Allocator, offset: i32) !void {
    const abs: u32 = if (offset < 0) @intCast(-offset) else @intCast(offset);
    const h = abs / 3600;
    const m = (abs % 3600) / 60;
    var tmp: [8]u8 = undefined;
    const s = std.fmt.bufPrint(&tmp, "{c}{d:0>2}:{d:0>2}", .{
        @as(u8, if (offset < 0) '-' else '+'),
        h,
        m,
    }) catch "+00:00";
    try buf.appendSlice(a, s);
}

fn appendOffsetCompact(buf: *std.ArrayListUnmanaged(u8), a: Allocator, offset: i32) !void {
    const abs: u32 = if (offset < 0) @intCast(-offset) else @intCast(offset);
    const h = abs / 3600;
    const m = (abs % 3600) / 60;
    var tmp: [8]u8 = undefined;
    const s = std.fmt.bufPrint(&tmp, "{c}{d:0>2}{d:0>2}", .{
        @as(u8, if (offset < 0) '-' else '+'),
        h,
        m,
    }) catch "+0000";
    try buf.appendSlice(a, s);
}

fn native_date(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.literal("");
    const format = args[0].string.bytes();
    const timestamp: i64 = if (args.len >= 2) Value.toInt(args[1]) else std.time.timestamp();
    return formatTimestamp(ctx, timestamp, format);
}

fn native_mktime(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const hour: i64 = if (args.len > 0) Value.toInt(args[0]) else 0;
    const min: i64 = if (args.len > 1) Value.toInt(args[1]) else 0;
    const sec: i64 = if (args.len > 2) Value.toInt(args[2]) else 0;
    const month: i64 = if (args.len > 3) Value.toInt(args[3]) else 1;
    const day: i64 = if (args.len > 4) Value.toInt(args[4]) else 1;
    const year: i64 = if (args.len > 5) Value.toInt(args[5]) else 1970;
    var ts = dateToTimestamp(year, month, day, hour, min, sec);
    ts -= @as(i64, tzOffsetForWallByName(ctx.allocator, ctx.vm.default_tz_name, ts));
    return NativeResult.scalar(.{ .int = ts });
}

fn native_gmmktime(_: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const hour: i64 = if (args.len > 0) Value.toInt(args[0]) else 0;
    const min: i64 = if (args.len > 1) Value.toInt(args[1]) else 0;
    const sec: i64 = if (args.len > 2) Value.toInt(args[2]) else 0;
    const month: i64 = if (args.len > 3) Value.toInt(args[3]) else 1;
    const day: i64 = if (args.len > 4) Value.toInt(args[4]) else 1;
    const year: i64 = if (args.len > 5) Value.toInt(args[5]) else 1970;
    return NativeResult.scalar(.{ .int = dateToTimestamp(year, month, day, hour, min, sec) });
}

fn native_strtotime(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.scalar(.{ .bool = false });
    const input = args[0].string.bytes();
    if (input.len == 0) return NativeResult.scalar(.{ .bool = false });
    const base: i64 = if (args.len >= 2 and args[1] != .null) Value.toInt(args[1]) else std.time.timestamp();
    const ts = (try strtotimeIn(ctx, input, base)) orelse return NativeResult.scalar(.{ .bool = false });
    return NativeResult.scalar(.{ .int = ts });
}

fn native_time(_: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    return NativeResult.scalar(.{ .int = std.time.timestamp() });
}

fn native_checkdate(_: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 3) return NativeResult.scalar(.{ .bool = false });
    const month = Value.toInt(args[0]);
    const day = Value.toInt(args[1]);
    const year = Value.toInt(args[2]);
    if (year < 1 or year > 32767 or month < 1 or month > 12 or day < 1) return NativeResult.scalar(.{ .bool = false });
    const days_in_month = [_]i64{ 31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 };
    var max_day = days_in_month[@intCast(month - 1)];
    if (month == 2) {
        const y: u32 = @intCast(year);
        if (y % 4 == 0 and (y % 100 != 0 or y % 400 == 0)) max_day = 29;
    }
    return NativeResult.scalar(.{ .bool = day <= max_day });
}

fn native_cal_days_in_month(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    // cal_days_in_month($calendar, $month, $year). only CAL_GREGORIAN (0) is
    // commonly used; zphp treats every calendar id as Gregorian
    if (args.len < 3) return NativeResult.scalar(.{ .bool = false });
    const month = Value.toInt(args[1]);
    const year = Value.toInt(args[2]);
    if (month < 1 or month > 12) {
        try ctx.vm.setPendingException("ValueError", "Invalid date");
        return error.RuntimeError;
    }
    return NativeResult.scalar(.{ .int = daysInMonth(month, year) });
}

fn native_date_parse(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.scalar(.{ .bool = false });
    var p = try parseDate(ctx, args[0].string.bytes());
    defer p.deinit(ctx.allocator);
    return parsedTimeArray(ctx, &p);
}

fn native_date_parse_from_format(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .string or args[1] != .string) return NativeResult.scalar(.{ .bool = false });
    const format = args[0].string.bytes();
    const datetime = args[1].string.bytes();

    var year: ?i64 = null;
    var month: ?i64 = null;
    var day: ?i64 = null;
    var hour: ?i64 = null;
    var minute: ?i64 = null;
    var second: ?i64 = null;
    var fraction: ?f64 = null;
    var is_pm: ?bool = null;
    var hour_is_12: bool = false;

    var errors_buf: [16][]const u8 = undefined;
    var n_errors: usize = 0;

    var fi: usize = 0;
    var di: usize = 0;
    while (fi < format.len) : (fi += 1) {
        const c = format[fi];
        switch (c) {
            'Y' => {
                if (di + 4 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 4], 10)) |v| {
                        year = v;
                        di += 4;
                    } else |_| {
                        if (n_errors < errors_buf.len) {
                            errors_buf[n_errors] = "A four digit year could not be found";
                            n_errors += 1;
                        }
                    }
                } else if (n_errors < errors_buf.len) {
                    errors_buf[n_errors] = "A four digit year could not be found";
                    n_errors += 1;
                }
            },
            'y' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |yy| {
                        year = if (yy < 70) 2000 + yy else 1900 + yy;
                        di += 2;
                    } else |_| {}
                }
            },
            'm' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        month = v;
                        di += 2;
                    } else |_| {}
                }
            },
            'n' => {
                if (takeDigits(datetime, di, 1, 2)) |t| {
                    month = t.value;
                    di = t.next;
                }
            },
            'd' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        day = v;
                        di += 2;
                    } else |_| {}
                }
            },
            'j' => {
                if (takeDigits(datetime, di, 1, 2)) |t| {
                    day = t.value;
                    di = t.next;
                }
            },
            'H' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        hour = v;
                        di += 2;
                    } else |_| {}
                }
            },
            'G' => {
                if (takeDigits(datetime, di, 1, 2)) |t| {
                    hour = t.value;
                    di = t.next;
                }
            },
            'h' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        hour = v;
                        di += 2;
                        hour_is_12 = true;
                    } else |_| {}
                }
            },
            'g' => {
                if (takeDigits(datetime, di, 1, 2)) |t| {
                    hour = t.value;
                    di = t.next;
                    hour_is_12 = true;
                }
            },
            'i' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        minute = v;
                        di += 2;
                    } else |_| {}
                }
            },
            's' => {
                if (di + 2 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 2], 10)) |v| {
                        second = v;
                        di += 2;
                    } else |_| {}
                }
            },
            'u' => {
                // microseconds, variable digits
                var end = di;
                while (end < datetime.len and datetime[end] >= '0' and datetime[end] <= '9') end += 1;
                if (end > di) {
                    const us = std.fmt.parseInt(i64, datetime[di..end], 10) catch 0;
                    const digits = end - di;
                    var divisor: f64 = 1;
                    var dd: usize = 0;
                    while (dd < digits) : (dd += 1) divisor *= 10;
                    fraction = @as(f64, @floatFromInt(us)) / divisor;
                    di = end;
                }
            },
            'v' => {
                // milliseconds 3 digits
                if (di + 3 <= datetime.len) {
                    if (std.fmt.parseInt(i64, datetime[di .. di + 3], 10)) |ms| {
                        fraction = @as(f64, @floatFromInt(ms)) / 1000.0;
                        di += 3;
                    } else |_| {}
                }
            },
            'a', 'A' => {
                if (di + 2 <= datetime.len) {
                    const tok = datetime[di .. di + 2];
                    if (std.ascii.eqlIgnoreCase(tok, "am")) {
                        is_pm = false;
                        di += 2;
                    } else if (std.ascii.eqlIgnoreCase(tok, "pm")) {
                        is_pm = true;
                        di += 2;
                    }
                }
            },
            'D', 'l' => {
                // skip alphabetic day name
                while (di < datetime.len and std.ascii.isAlphabetic(datetime[di])) di += 1;
            },
            'M', 'F' => {
                // month name - look up
                var end = di;
                while (end < datetime.len and std.ascii.isAlphabetic(datetime[end])) end += 1;
                if (end > di) {
                    const name = datetime[di..end];
                    const months = [_][]const u8{ "january", "february", "march", "april", "may", "june", "july", "august", "september", "october", "november", "december" };
                    for (months, 0..) |mn, idx| {
                        if (std.ascii.startsWithIgnoreCase(mn, name) and (name.len == mn.len or name.len == 3)) {
                            month = @intCast(idx + 1);
                            break;
                        }
                    }
                    di = end;
                }
            },
            ' ', '\t' => {
                while (di < datetime.len and (datetime[di] == ' ' or datetime[di] == '\t')) di += 1;
            },
            '\\' => {
                fi += 1;
                if (fi < format.len and di < datetime.len and datetime[di] == format[fi]) di += 1;
            },
            '!' => {
                if (year == null) year = 1970;
                if (month == null) month = 1;
                if (day == null) day = 1;
                if (hour == null) hour = 0;
                if (minute == null) minute = 0;
                if (second == null) second = 0;
                if (fraction == null) fraction = 0;
            },
            '|' => {
                if (year == null) year = 1970;
                if (month == null) month = 1;
                if (day == null) day = 1;
                if (hour == null) hour = 0;
                if (minute == null) minute = 0;
                if (second == null) second = 0;
            },
            else => {
                // literal char: PHP advances input if it matches; mismatches
                // are silent (no errors row) for ordinary punctuation
                if (di < datetime.len and datetime[di] == c) di += 1;
            },
        }
    }

    if (hour_is_12) {
        if (is_pm) |pm| {
            if (pm and (hour orelse 0) < 12) hour = (hour orelse 0) + 12 else if (!pm and (hour orelse 0) == 12) hour = 0;
        }
    }

    // PHP: if any time component was parsed, the other time components default
    // to 0 (rather than remaining unset). hour-implies-min-sec-fraction-min,
    // etc. all of h/m/s/fraction become 0 when ANY time field is touched
    const any_time = hour != null or minute != null or second != null or fraction != null;
    if (any_time) {
        if (hour == null) hour = 0;
        if (minute == null) minute = 0;
        if (second == null) second = 0;
        if (fraction == null) fraction = 0;
    }

    return buildDateParseResultOpt(ctx, year, month, day, hour, minute, second, fraction, errors_buf[0..n_errors]);
}

fn buildDateParseResultOpt(ctx: *NativeContext, year: ?i64, month: ?i64, day: ?i64, hour: ?i64, minute: ?i64, second: ?i64, fraction: ?f64, errors: []const []const u8) RuntimeError!NativeResult {
    var arr = try ctx.createArray();
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("year") }, if (year) |y| .{ .int = y } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("month") }, if (month) |m| .{ .int = m } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("day") }, if (day) |d| .{ .int = d } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("hour") }, if (hour) |h| .{ .int = h } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("minute") }, if (minute) |m| .{ .int = m } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("second") }, if (second) |s| .{ .int = s } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("fraction") }, if (fraction) |f| .{ .float = f } else .{ .bool = false });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("warning_count") }, .{ .int = 0 });
    const warns = try ctx.vm.allocArray();
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("warnings") }, .{ .array = warns });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("error_count") }, .{ .int = @intCast(errors.len) });
    const errs = try ctx.vm.allocArray();
    for (errors, 0..) |e, i| try errs.set(ctx.allocator, .{ .int = @intCast(i) }, .{ .string = Value.String.borrowed(e) });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("errors") }, .{ .array = errs });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("is_localtime") }, .{ .bool = false });
    return NativeResult.borrowed(.{ .array = arr });
}

fn native_getdate(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const timestamp: i64 = if (args.len >= 1 and args[0] != .null) Value.toInt(args[0]) else std.time.timestamp();
    const lp = localParts(ctx.allocator, timestamp, ctx.vm.default_tz_name);
    const weekdays = [_][]const u8{ "Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday" };
    const months = [_][]const u8{ "January", "February", "March", "April", "May", "June", "July", "August", "September", "October", "November", "December" };

    var arr = try ctx.createArray();
    const ints = [_]struct { []const u8, i64 }{
        .{ "seconds", lp.dc.sec },
        .{ "minutes", lp.dc.min },
        .{ "hours", lp.dc.hour },
        .{ "mday", lp.dc.day },
        .{ "wday", lp.dow },
        .{ "mon", lp.dc.month },
        .{ "year", lp.dc.year },
        .{ "yday", lp.yday },
    };
    for (ints) |f| try arr.set(ctx.allocator, .{ .string = Value.String.borrowed(f[0]) }, .{ .int = f[1] });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("weekday") }, .{ .string = Value.String.borrowed(weekdays[@intCast(lp.dow)]) });
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed("month") }, .{ .string = Value.String.borrowed(months[@intCast(lp.dc.month - 1)]) });
    try arr.set(ctx.allocator, .{ .int = 0 }, .{ .int = timestamp });
    return NativeResult.borrowed(.{ .array = arr });
}

fn native_hrtime(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const as_int = args.len >= 1 and args[0].isTruthy();
    const ns = std.time.nanoTimestamp();
    if (as_int) {
        return NativeResult.scalar(.{ .int = @intCast(ns) });
    }
    const secs: i64 = @intCast(@divTrunc(ns, 1_000_000_000));
    const remainder: i64 = @intCast(@mod(ns, 1_000_000_000));
    var arr = try ctx.createArray();
    try arr.append(ctx.allocator, .{ .int = secs });
    try arr.append(ctx.allocator, .{ .int = remainder });
    return NativeResult.borrowed(.{ .array = arr });
}

fn native_microtime(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const as_float = args.len >= 1 and args[0].isTruthy();
    const ns = std.time.nanoTimestamp();
    if (as_float) {
        const secs: f64 = @as(f64, @floatFromInt(ns)) / 1_000_000_000.0;
        return NativeResult.scalar(.{ .float = secs });
    }
    const ts: i64 = @intCast(@divTrunc(ns, 1_000_000_000));
    const usec: i64 = @intCast(@divTrunc(@mod(ns, 1_000_000_000), 1_000));
    var buf = std.ArrayListUnmanaged(u8){};
    defer buf.deinit(ctx.allocator);
    var tmp: [32]u8 = undefined;
    try buf.appendSlice(ctx.allocator, "0.");
    const usec_str = std.fmt.bufPrint(&tmp, "{d:0>6}", .{@as(u64, @intCast(if (usec < 0) -usec else usec))}) catch "000000";
    try buf.appendSlice(ctx.allocator, usec_str);
    try buf.appendSlice(ctx.allocator, " ");
    const ts_str = std.fmt.bufPrint(&tmp, "{d}", .{ts}) catch "0";
    try buf.appendSlice(ctx.allocator, ts_str);
    const result = try buf.toOwnedSlice(ctx.allocator);
    return NativeResult.takeString(try Value.String.adopt(ctx.allocator, result));
}

// date/time utilities

pub fn dateToTimestamp(year: i64, month: i64, day: i64, hour: i64, min: i64, sec: i64) i64 {
    // normalize month overflow/underflow (e.g. month 13 -> january next year)
    const adj_month = @mod(month - 1, @as(i64, 12)) + 1;
    const adj_year = year + @divFloor(month - 1, @as(i64, 12));
    const m = adj_month;
    const y = adj_year - @as(i64, if (m <= 2) 1 else 0);
    const era: i64 = @divFloor(if (y >= 0) y else y - 399, 400);
    const yoe: i64 = y - era * 400;
    const mp: i64 = if (m > 2) m - 3 else m + 9;
    const doy = @divFloor(153 * mp + 2, 5) + day - 1;
    const doe = yoe * 365 + @divFloor(yoe, 4) - @divFloor(yoe, 100) + doy;
    const days = era * 146097 + doe - 719468;
    return days * 86400 + hour * 3600 + min * 60 + sec;
}

const DateComponents = struct { year: i64, month: i64, day: i64, hour: i64, min: i64, sec: i64 };

const FmtDaySec = struct {
    h: u32,
    mi: u32,
    s: u32,
    pub fn getHoursIntoDay(self: @This()) u32 {
        return self.h;
    }
    pub fn getMinutesIntoHour(self: @This()) u32 {
        return self.mi;
    }
    pub fn getSecondsIntoMinute(self: @This()) u32 {
        return self.s;
    }
};
const FmtEpochDay = struct { day: i64 };
const FmtYearDay = struct { year: i64 };
const FmtMonth = struct {
    v: u32,
    pub fn numeric(s: @This()) u32 {
        return s.v;
    }
};
const FmtMonthDay = struct { month: FmtMonth, day_index: u32 };

const IsoWeek = struct { year: i64, week: u32 };

fn isoWeek(day_num: i64) IsoWeek {
    // iso 8601 week-numbering: week 1 contains the year's first Thursday.
    // the iso year is the calendar year of the Thursday in the same week.
    const dow = @mod(day_num + 3, 7); // 0=mon
    const thu = day_num + 3 - dow;
    const thu_secs = thu * 86400;
    const thu_dc = baseComponents(thu_secs);
    const jan1_ts = dateToTimestamp(thu_dc.year, 1, 1, 0, 0, 0);
    const jan1_day = @divFloor(jan1_ts, 86400);
    const week_num: u32 = @intCast(@divFloor(thu - jan1_day, 7) + 1);
    return .{ .year = thu_dc.year, .week = week_num };
}

fn baseComponents(base: i64) DateComponents {
    // Howard Hinnant's civil_from_days, works for any year including pre-1970
    // seconds-of-day in [0,86400); @mod keeps the extreme timestamps from
    // overflowing days * 86400
    const days = @divFloor(base, 86400);
    const sod: i64 = @mod(base, 86400);
    const z = days + 719468;
    const era = @divFloor(z, 146097);
    const doe: i64 = z - era * 146097;
    const yoe: i64 = @divFloor(doe - @divFloor(doe, 1460) + @divFloor(doe, 36524) - @divFloor(doe, 146096), 365);
    const y_civil: i64 = yoe + era * 400;
    const doy: i64 = doe - (365 * yoe + @divFloor(yoe, 4) - @divFloor(yoe, 100));
    const mp: i64 = @divFloor(5 * doy + 2, 153);
    const d_civil: i64 = doy - @divFloor(153 * mp + 2, 5) + 1;
    const m_civil: i64 = if (mp < 10) mp + 3 else mp - 9;
    const year: i64 = if (m_civil <= 2) y_civil + 1 else y_civil;
    return .{
        .year = year,
        .month = m_civil,
        .day = d_civil,
        .hour = @divFloor(sod, 3600),
        .min = @divFloor(@mod(sod, 3600), 60),
        .sec = @mod(sod, 60),
    };
}

fn parseMonthName(s: []const u8) ?i64 {
    const months = [_][]const u8{ "january", "february", "march", "april", "may", "june", "july", "august", "september", "october", "november", "december" };
    for (months, 1..) |name, i| {
        if (s.len >= name.len and eqlLower(s[0..name.len], name)) return @intCast(i);
    }
    return null;
}

fn monthNameLen(s: []const u8) usize {
    const months = [_][]const u8{ "january", "february", "march", "april", "may", "june", "july", "august", "september", "october", "november", "december" };
    for (months) |name| {
        if (s.len >= name.len and eqlLower(s[0..name.len], name)) return name.len;
    }
    return 0;
}

fn eqlLower(a: []const u8, lower_b: []const u8) bool {
    if (a.len != lower_b.len) return false;
    for (a, lower_b) |ca, cb| {
        const la: u8 = if (ca >= 'A' and ca <= 'Z') ca + 32 else ca;
        if (la != cb) return false;
    }
    return true;
}

fn daysInMonth(month: i64, year: i64) i64 {
    const days = [_]i64{ 31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 };
    const m: usize = @intCast(if (month < 1) 0 else if (month > 12) 11 else month - 1);
    var d = days[m];
    if (m == 1) {
        const y = if (year < 0) -year else year;
        if (@mod(y, 4) == 0 and (@mod(y, 100) != 0 or @mod(y, 400) == 0)) d = 29;
    }
    return d;
}

const DstRule = enum { none, us, eu, au, nz };

const TzEntry = struct {
    name: []const u8,
    std_offset: i32,
    dst_offset: i32,
    dst_rule: DstRule,
    std_abbrev: []const u8,
    dst_abbrev: []const u8,
};

// us dst: second sunday of march 2:00 -> first sunday of november 2:00
// eu dst: last sunday of march 1:00 utc -> last sunday of october 1:00 utc
const tz_table = [_]TzEntry{
    // north america
    .{ .name = "america/new_york", .std_offset = -5 * 3600, .dst_offset = -4 * 3600, .dst_rule = .us, .std_abbrev = "EST", .dst_abbrev = "EDT" },
    .{ .name = "america/chicago", .std_offset = -6 * 3600, .dst_offset = -5 * 3600, .dst_rule = .us, .std_abbrev = "CST", .dst_abbrev = "CDT" },
    .{ .name = "america/denver", .std_offset = -7 * 3600, .dst_offset = -6 * 3600, .dst_rule = .us, .std_abbrev = "MST", .dst_abbrev = "MDT" },
    .{ .name = "america/los_angeles", .std_offset = -8 * 3600, .dst_offset = -7 * 3600, .dst_rule = .us, .std_abbrev = "PST", .dst_abbrev = "PDT" },
    .{ .name = "america/anchorage", .std_offset = -9 * 3600, .dst_offset = -8 * 3600, .dst_rule = .us, .std_abbrev = "AKST", .dst_abbrev = "AKDT" },
    .{ .name = "america/phoenix", .std_offset = -7 * 3600, .dst_offset = -7 * 3600, .dst_rule = .none, .std_abbrev = "MST", .dst_abbrev = "MST" },
    .{ .name = "america/toronto", .std_offset = -5 * 3600, .dst_offset = -4 * 3600, .dst_rule = .us, .std_abbrev = "EST", .dst_abbrev = "EDT" },
    .{ .name = "america/vancouver", .std_offset = -8 * 3600, .dst_offset = -7 * 3600, .dst_rule = .us, .std_abbrev = "PST", .dst_abbrev = "PDT" },
    .{ .name = "america/mexico_city", .std_offset = -6 * 3600, .dst_offset = -6 * 3600, .dst_rule = .none, .std_abbrev = "CST", .dst_abbrev = "CST" },
    .{ .name = "america/sao_paulo", .std_offset = -3 * 3600, .dst_offset = -3 * 3600, .dst_rule = .none, .std_abbrev = "-03", .dst_abbrev = "-03" },
    .{ .name = "america/argentina/buenos_aires", .std_offset = -3 * 3600, .dst_offset = -3 * 3600, .dst_rule = .none, .std_abbrev = "-03", .dst_abbrev = "-03" },
    .{ .name = "pacific/honolulu", .std_offset = -10 * 3600, .dst_offset = -10 * 3600, .dst_rule = .none, .std_abbrev = "HST", .dst_abbrev = "HST" },
    // europe
    .{ .name = "europe/london", .std_offset = 0, .dst_offset = 3600, .dst_rule = .eu, .std_abbrev = "GMT", .dst_abbrev = "BST" },
    .{ .name = "europe/paris", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/berlin", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/amsterdam", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/brussels", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/madrid", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/rome", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/zurich", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/vienna", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    .{ .name = "europe/moscow", .std_offset = 3 * 3600, .dst_offset = 3 * 3600, .dst_rule = .none, .std_abbrev = "MSK", .dst_abbrev = "MSK" },
    .{ .name = "europe/istanbul", .std_offset = 3 * 3600, .dst_offset = 3 * 3600, .dst_rule = .none, .std_abbrev = "+03", .dst_abbrev = "+03" },
    .{ .name = "europe/athens", .std_offset = 2 * 3600, .dst_offset = 3 * 3600, .dst_rule = .eu, .std_abbrev = "EET", .dst_abbrev = "EEST" },
    .{ .name = "europe/helsinki", .std_offset = 2 * 3600, .dst_offset = 3 * 3600, .dst_rule = .eu, .std_abbrev = "EET", .dst_abbrev = "EEST" },
    .{ .name = "europe/bucharest", .std_offset = 2 * 3600, .dst_offset = 3 * 3600, .dst_rule = .eu, .std_abbrev = "EET", .dst_abbrev = "EEST" },
    .{ .name = "europe/lisbon", .std_offset = 0, .dst_offset = 3600, .dst_rule = .eu, .std_abbrev = "WET", .dst_abbrev = "WEST" },
    .{ .name = "europe/warsaw", .std_offset = 3600, .dst_offset = 2 * 3600, .dst_rule = .eu, .std_abbrev = "CET", .dst_abbrev = "CEST" },
    // asia
    .{ .name = "asia/tokyo", .std_offset = 9 * 3600, .dst_offset = 9 * 3600, .dst_rule = .none, .std_abbrev = "JST", .dst_abbrev = "JST" },
    .{ .name = "asia/shanghai", .std_offset = 8 * 3600, .dst_offset = 8 * 3600, .dst_rule = .none, .std_abbrev = "CST", .dst_abbrev = "CST" },
    .{ .name = "asia/hong_kong", .std_offset = 8 * 3600, .dst_offset = 8 * 3600, .dst_rule = .none, .std_abbrev = "HKT", .dst_abbrev = "HKT" },
    .{ .name = "asia/singapore", .std_offset = 8 * 3600, .dst_offset = 8 * 3600, .dst_rule = .none, .std_abbrev = "+08", .dst_abbrev = "+08" },
    .{ .name = "asia/kolkata", .std_offset = 5 * 3600 + 1800, .dst_offset = 5 * 3600 + 1800, .dst_rule = .none, .std_abbrev = "IST", .dst_abbrev = "IST" },
    .{ .name = "asia/dubai", .std_offset = 4 * 3600, .dst_offset = 4 * 3600, .dst_rule = .none, .std_abbrev = "+04", .dst_abbrev = "+04" },
    .{ .name = "asia/seoul", .std_offset = 9 * 3600, .dst_offset = 9 * 3600, .dst_rule = .none, .std_abbrev = "KST", .dst_abbrev = "KST" },
    .{ .name = "asia/bangkok", .std_offset = 7 * 3600, .dst_offset = 7 * 3600, .dst_rule = .none, .std_abbrev = "+07", .dst_abbrev = "+07" },
    .{ .name = "asia/jakarta", .std_offset = 7 * 3600, .dst_offset = 7 * 3600, .dst_rule = .none, .std_abbrev = "WIB", .dst_abbrev = "WIB" },
    .{ .name = "asia/tehran", .std_offset = 3 * 3600 + 1800, .dst_offset = 3 * 3600 + 1800, .dst_rule = .none, .std_abbrev = "+0330", .dst_abbrev = "+0330" },
    .{ .name = "asia/karachi", .std_offset = 5 * 3600, .dst_offset = 5 * 3600, .dst_rule = .none, .std_abbrev = "PKT", .dst_abbrev = "PKT" },
    .{ .name = "asia/dhaka", .std_offset = 6 * 3600, .dst_offset = 6 * 3600, .dst_rule = .none, .std_abbrev = "+06", .dst_abbrev = "+06" },
    .{ .name = "asia/kathmandu", .std_offset = 5 * 3600 + 2700, .dst_offset = 5 * 3600 + 2700, .dst_rule = .none, .std_abbrev = "+0545", .dst_abbrev = "+0545" },
    // oceania
    .{ .name = "australia/sydney", .std_offset = 10 * 3600, .dst_offset = 11 * 3600, .dst_rule = .au, .std_abbrev = "AEST", .dst_abbrev = "AEDT" },
    .{ .name = "australia/melbourne", .std_offset = 10 * 3600, .dst_offset = 11 * 3600, .dst_rule = .au, .std_abbrev = "AEST", .dst_abbrev = "AEDT" },
    .{ .name = "australia/hobart", .std_offset = 10 * 3600, .dst_offset = 11 * 3600, .dst_rule = .au, .std_abbrev = "AEST", .dst_abbrev = "AEDT" },
    .{ .name = "australia/brisbane", .std_offset = 10 * 3600, .dst_offset = 10 * 3600, .dst_rule = .none, .std_abbrev = "AEST", .dst_abbrev = "AEST" },
    .{ .name = "australia/perth", .std_offset = 8 * 3600, .dst_offset = 8 * 3600, .dst_rule = .none, .std_abbrev = "AWST", .dst_abbrev = "AWST" },
    .{ .name = "pacific/auckland", .std_offset = 12 * 3600, .dst_offset = 13 * 3600, .dst_rule = .nz, .std_abbrev = "NZST", .dst_abbrev = "NZDT" },
    // africa
    // africa/cairo intentionally NOT tabled: Egypt's DST is irregular (announced
    // yearly, Fri/Thu transitions - not a Sunday rule), so it resolves via TZif
    // for correctness (the old .none entry was wrong every summer: +02 not +03)
    .{ .name = "africa/lagos", .std_offset = 3600, .dst_offset = 3600, .dst_rule = .none, .std_abbrev = "WAT", .dst_abbrev = "WAT" },
    .{ .name = "africa/johannesburg", .std_offset = 2 * 3600, .dst_offset = 2 * 3600, .dst_rule = .none, .std_abbrev = "SAST", .dst_abbrev = "SAST" },
    .{ .name = "africa/nairobi", .std_offset = 3 * 3600, .dst_offset = 3 * 3600, .dst_rule = .none, .std_abbrev = "EAT", .dst_abbrev = "EAT" },
    // aliases
    .{ .name = "utc", .std_offset = 0, .dst_offset = 0, .dst_rule = .none, .std_abbrev = "UTC", .dst_abbrev = "UTC" },
    .{ .name = "gmt", .std_offset = 0, .dst_offset = 0, .dst_rule = .none, .std_abbrev = "GMT", .dst_abbrev = "GMT" },
    .{ .name = "us/eastern", .std_offset = -5 * 3600, .dst_offset = -4 * 3600, .dst_rule = .us, .std_abbrev = "EST", .dst_abbrev = "EDT" },
    .{ .name = "us/central", .std_offset = -6 * 3600, .dst_offset = -5 * 3600, .dst_rule = .us, .std_abbrev = "CST", .dst_abbrev = "CDT" },
    .{ .name = "us/mountain", .std_offset = -7 * 3600, .dst_offset = -6 * 3600, .dst_rule = .us, .std_abbrev = "MST", .dst_abbrev = "MDT" },
    .{ .name = "us/pacific", .std_offset = -8 * 3600, .dst_offset = -7 * 3600, .dst_rule = .us, .std_abbrev = "PST", .dst_abbrev = "PDT" },
    .{ .name = "est", .std_offset = -5 * 3600, .dst_offset = -5 * 3600, .dst_rule = .none, .std_abbrev = "EST", .dst_abbrev = "EST" },
    .{ .name = "mst", .std_offset = -7 * 3600, .dst_offset = -7 * 3600, .dst_rule = .none, .std_abbrev = "MST", .dst_abbrev = "MST" },
    .{ .name = "hst", .std_offset = -10 * 3600, .dst_offset = -10 * 3600, .dst_rule = .none, .std_abbrev = "HST", .dst_abbrev = "HST" },
};

fn lookupTimezone(name: []const u8) ?TzEntry {
    // try fixed offset first: +05:30, -08:00, +0530, etc
    if (name.len >= 5 and (name[0] == '+' or name[0] == '-')) {
        const offset = parseFixedOffset(name) orelse return null;
        return TzEntry{
            .name = name,
            .std_offset = offset,
            .dst_offset = offset,
            .dst_rule = .none,
            .std_abbrev = name,
            .dst_abbrev = name,
        };
    }

    for (tz_table) |entry| {
        if (eqlLower(name, entry.name)) return entry;
    }
    return null;
}

// ---- TZif (system zoneinfo) fallback for zones not in the hardcoded table ----
// parses /usr/share/zoneinfo/<Zone> (the IANA tzdb binary) to give correct
// offsets/DST/abbreviations/historical transitions for ANY zone. the hardcoded
// table is tried first (fast, no I/O, works where zoneinfo is absent e.g. musl);
// this only runs for zones the table doesn't cover.

const TzifRes = struct { offset: i32, is_dst: bool, abbrev_buf: [16]u8 = undefined, abbrev_len: u8 = 0 };

fn tzNameValid(name: []const u8) bool {
    if (name.len == 0 or name.len > 64) return false;
    if (name[0] == '/' or name[0] == '+' or name[0] == '-') return false;
    if (std.mem.indexOf(u8, name, "..") != null) return false;
    for (name) |c| {
        if (!(std.ascii.isAlphanumeric(c) or c == '/' or c == '_' or c == '-' or c == '+')) return false;
    }
    return true;
}

fn readZoneInfo(allocator: Allocator, name: []const u8) ?[]u8 {
    if (!tzNameValid(name)) return null;
    const dirs = [_][]const u8{ "/usr/share/zoneinfo/", "/etc/zoneinfo/", "/usr/lib/zoneinfo/", "/usr/share/lib/zoneinfo/" };
    for (dirs) |dir| {
        var pathbuf: [320]u8 = undefined;
        const path = std.fmt.bufPrint(&pathbuf, "{s}{s}", .{ dir, name }) catch continue;
        const file = std.fs.openFileAbsolute(path, .{}) catch continue;
        defer file.close();
        const data = file.readToEndAlloc(allocator, 1 << 20) catch continue;
        return data;
    }
    return null;
}

// embedded IANA tzdb, used when the host has no /usr/share/zoneinfo (musl /
// Alpine). bundles the TZif bytes for the zones zphp advertises via
// timezone_identifiers_list(). regenerate with scripts/gen-tzdata. format:
// "ZTZ1" | u32 count | { u16 name_len, name, u32 data_len, tzif_bytes }*
const tzdata_gz = @embedFile("tzdata.bin.gz");

// the decompressed blob lives for the process lifetime (read-only after init),
// so it's allocated from the page allocator rather than a request arena and
// never freed. guarded so concurrent first-touch in threaded serve workers
// can't double-decompress
var g_tz_mutex: std.Thread.Mutex = .{};
var g_tz_blob: ?[]const u8 = null;
var g_tz_init = false;

fn inflateGzipPage(input: []const u8) ?[]u8 {
    var stream: zlib.z_stream = std.mem.zeroes(zlib.z_stream);
    // 15 window bits + 16 selects gzip framing
    if (zlib.inflateInit2_(&stream, 15 + 16, zlib.zlibVersion(), @sizeOf(zlib.z_stream)) != zlib.Z_OK) return null;
    defer _ = zlib.inflateEnd(&stream);
    const a = std.heap.page_allocator;
    var out = std.ArrayListUnmanaged(u8){};
    errdefer out.deinit(a);
    stream.next_in = @constCast(input.ptr);
    stream.avail_in = @intCast(input.len);
    var chunk: [32 * 1024]u8 = undefined;
    while (true) {
        stream.next_out = &chunk;
        stream.avail_out = chunk.len;
        const rc = zlib.inflate(&stream, zlib.Z_NO_FLUSH);
        const produced = chunk.len - stream.avail_out;
        if (produced > 0) out.appendSlice(a, chunk[0..produced]) catch return null;
        if (rc == zlib.Z_STREAM_END) break;
        if (rc != zlib.Z_OK) return null;
        if (stream.avail_in == 0 and produced == 0) break;
    }
    return out.toOwnedSlice(a) catch null;
}

fn embeddedBlob() ?[]const u8 {
    g_tz_mutex.lock();
    defer g_tz_mutex.unlock();
    if (g_tz_init) return g_tz_blob;
    g_tz_init = true;
    g_tz_blob = inflateGzipPage(tzdata_gz);
    return g_tz_blob;
}

// borrowed TZif bytes for `name` from the embedded blob (valid for the process
// lifetime, never freed), or null. exact-case match, mirroring readZoneInfo's
// case-sensitive path lookup
fn embeddedZoneInfo(name: []const u8) ?[]const u8 {
    const z = embeddedZone(name, false) orelse return null;
    return z.data;
}

// zone ids match case-insensitively, as in php ("america/new_york")
fn embeddedZone(name: []const u8, ignore_case: bool) ?struct { name: []const u8, data: []const u8 } {
    if (!tzNameValid(name)) return null;
    const blob = embeddedBlob() orelse return null;
    if (blob.len < 8 or !std.mem.eql(u8, blob[0..4], "ZTZ1")) return null;
    var off: usize = 4;
    const count = std.mem.readInt(u32, blob[off..][0..4], .little);
    off += 4;
    var i: u32 = 0;
    while (i < count) : (i += 1) {
        if (off + 2 > blob.len) return null;
        const nl = std.mem.readInt(u16, blob[off..][0..2], .little);
        off += 2;
        if (off + nl + 4 > blob.len) return null;
        const ename = blob[off .. off + nl];
        off += nl;
        const dl = std.mem.readInt(u32, blob[off..][0..4], .little);
        off += 4;
        if (off + dl > blob.len) return null;
        const data = blob[off .. off + dl];
        off += dl;
        const hit = if (ignore_case) std.ascii.eqlIgnoreCase(ename, name) else std.mem.eql(u8, ename, name);
        if (hit) return .{ .name = ename, .data = data };
    }
    return null;
}

// TZif bytes for a zone: system zoneinfo first (owned, must be freed), then the
// embedded blob (borrowed, must NOT be freed). centralizes the system-then-
// embedded fallback so every call site handles ownership uniformly
const TzBytes = struct {
    bytes: []const u8,
    owned: bool,
    fn deinit(self: TzBytes, allocator: Allocator) void {
        if (self.owned) allocator.free(@constCast(self.bytes));
    }
};

// zone files are read once per process: tzdata does not change while zphp
// runs and every date() call in a timezone-aware app would otherwise reopen
// /usr/share/zoneinfo/<name>. entries are keyed by the requested name and
// never freed (a few KB per distinct zone); the mutex covers threaded serve
var zone_cache: std.StringHashMapUnmanaged([]const u8) = .{};
var zone_cache_mutex: std.Thread.Mutex = .{};

// the zone this thread resolved last; cached bytes are never freed, so a hit
// skips the shared lock and hash lookup that date code repeats per call
threadlocal var last_zone_name: [64]u8 = undefined;
threadlocal var last_zone_len: usize = 0;
threadlocal var last_zone_bytes: []const u8 = &.{};

fn resolveTzif(allocator: Allocator, name: []const u8) ?TzBytes {
    if (last_zone_len > 0 and std.mem.eql(u8, name, last_zone_name[0..last_zone_len])) return .{ .bytes = last_zone_bytes, .owned = false };
    zone_cache_mutex.lock();
    defer zone_cache_mutex.unlock();
    if (zone_cache.get(name)) |b| {
        if (name.len <= last_zone_name.len) {
            @memcpy(last_zone_name[0..name.len], name);
            last_zone_len = name.len;
            last_zone_bytes = b;
        }
        return .{ .bytes = b, .owned = false };
    }
    const bytes: []const u8 = readZoneInfo(std.heap.page_allocator, name) orelse embeddedZoneInfo(name) orelse blk: {
        // a differently-cased id resolves through its canonical spelling
        const z = embeddedZone(name, true) orelse return null;
        break :blk readZoneInfo(std.heap.page_allocator, z.name) orelse z.data;
    };
    const key = std.heap.page_allocator.dupe(u8, name) catch return .{ .bytes = bytes, .owned = false };
    zone_cache.put(std.heap.page_allocator, key, bytes) catch {};
    _ = allocator;
    return .{ .bytes = bytes, .owned = false };
}

test "embedded tzdata resolves non-table zones without system zoneinfo" {
    // exercises the musl/Alpine fallback path directly: decompress the embedded
    // blob, find the zone, parse its TZif. independent of the host filesystem so
    // it proves the fallback works where /usr/share/zoneinfo is absent.
    // 2026-01-01T00:00:00Z (well clear of any DST edge for these zones)
    const ts: i64 = 1767225600;

    // Kathmandu is the canonical odd offset: +5:45, no DST
    const kt = embeddedZoneInfo("Asia/Kathmandu") orelse return error.ZoneMissing;
    const ktr = tzifLookupUtc(kt, ts) orelse return error.TzifParseFailed;
    try std.testing.expectEqual(@as(i32, 5 * 3600 + 45 * 60), ktr.offset);

    // Chatham: +12:45 standard (NZ summer DST in January -> +13:45)
    const ch = embeddedZoneInfo("Pacific/Chatham") orelse return error.ZoneMissing;
    const chr = tzifLookupUtc(ch, ts) orelse return error.TzifParseFailed;
    try std.testing.expectEqual(@as(i32, 13 * 3600 + 45 * 60), chr.offset);

    // a zone that lives in the hardcoded table is still present in the blob
    const ny = embeddedZoneInfo("America/New_York") orelse return error.ZoneMissing;
    const nyr = tzifLookupUtc(ny, ts) orelse return error.TzifParseFailed;
    try std.testing.expectEqual(@as(i32, -5 * 3600), nyr.offset); // EST in January

    // bogus names resolve to nothing
    try std.testing.expect(embeddedZoneInfo("Not/AReal_Zone") == null);
}

fn firstNonDstType(bytes: []const u8, ttinfo_off: usize, typecnt: usize) u8 {
    var t: usize = 0;
    while (t < typecnt) : (t += 1) {
        if (bytes[ttinfo_off + t * 6 + 4] == 0) return @intCast(t);
    }
    return 0;
}

// ---- the TZif footer: a posix TZ rule ("EST5EDT,M3.2.0,M11.1.0") that
// governs every instant after the last listed transition (parse_posix.c) ----

const PosixSpec = struct {
    kind: enum { julian_no_feb29, julian_feb29, mwd } = .julian_feb29,
    days: i64 = 0,
    month: i64 = 0,
    week: i64 = 0,
    dow: i64 = 0,
    hour: i64 = 7200,
};

const PosixRule = struct {
    std_name: []const u8,
    std_offset: i64,
    dst_name: []const u8 = "",
    dst_offset: i64 = 0,
    begin: ?PosixSpec = null,
    end: ?PosixSpec = null,
};

const PosixReader = struct {
    s: []const u8,
    i: usize = 0,

    fn c(r: *const PosixReader) u8 {
        return if (r.i < r.s.len) r.s[r.i] else 0;
    }

    fn description(r: *PosixReader) ?[]const u8 {
        if (r.c() == '<') {
            r.i += 1;
            const begin = r.i;
            while (r.c() != 0 and r.c() != '>') r.i += 1;
            if (r.c() == 0) return null;
            const name = r.s[begin..r.i];
            r.i += 1;
            return if (name.len < 1) null else name;
        }
        const begin = r.i;
        while (std.ascii.isAlphabetic(r.c())) r.i += 1;
        return if (r.i == begin) null else r.s[begin..r.i];
    }

    fn number(r: *PosixReader) ?i64 {
        const begin = r.i;
        var acc: i64 = 0;
        while (std.ascii.isDigit(r.c())) {
            acc = acc *% 10 +% (r.c() - '0');
            r.i += 1;
        }
        return if (r.i == begin) null else acc;
    }

    // "5", "-3:30", "+2:00:15" read as a utc offset, which is the negation
    fn offset(r: *PosixReader) ?i64 {
        var bias: i64 = 1;
        if (r.c() == '+') {
            r.i += 1;
        } else if (r.c() == '-') {
            bias = -1;
            r.i += 1;
        }
        const hours = r.number() orelse return null;
        var minutes: i64 = 0;
        var seconds: i64 = 0;
        if (r.c() == ':') {
            r.i += 1;
            minutes = r.number() orelse return null;
        }
        if (r.c() == ':') {
            r.i += 1;
            seconds = r.number() orelse return null;
        }
        return -bias * (hours * 3600 + minutes * 60 + seconds);
    }

    fn spec(r: *PosixReader) ?PosixSpec {
        var sp = PosixSpec{};
        if (r.c() == 'M') {
            r.i += 1;
            sp.kind = .mwd;
            sp.month = r.number() orelse return null;
            if (r.c() != '.') return null;
            r.i += 1;
            sp.week = r.number() orelse return null;
            if (r.c() != '.') return null;
            r.i += 1;
            sp.dow = r.number() orelse return null;
        } else {
            if (r.c() == 'J') {
                sp.kind = .julian_no_feb29;
                r.i += 1;
            }
            sp.days = r.number() orelse return null;
        }
        if (r.c() == '/') {
            r.i += 1;
            sp.hour = -(r.offset() orelse return null);
        }
        return sp;
    }
};

fn parsePosixRule(text: []const u8) ?PosixRule {
    var r = PosixReader{ .s = text };
    var rule = PosixRule{ .std_name = r.description() orelse return null, .std_offset = r.offset() orelse return null };
    if (r.c() == 0) return rule;
    rule.dst_offset = rule.std_offset + 3600;
    rule.dst_name = r.description() orelse return null;
    if (r.c() != ',' and r.c() != 0) rule.dst_offset = r.offset() orelse return null;
    if (r.c() != ',') return null;
    r.i += 1;
    rule.begin = r.spec() orelse return null;
    if (r.c() != ',') return null;
    r.i += 1;
    rule.end = r.spec() orelse return null;
    if (r.c() != 0) return null;
    return rule;
}

const posix_month_lengths = [2][12]i64{
    .{ 31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 },
    .{ 31, 29, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 },
};

// seconds from the start of `year` to the transition day (tzcode's transtime)
fn posixTransition(sp: PosixSpec, year: i64) i64 {
    const leap: usize = if (dp.isLeap(year)) 1 else 0;
    switch (sp.kind) {
        .julian_no_feb29 => {
            var value = sp.days - 1;
            if (leap == 1 and sp.days >= 60) value += 1;
            return value *% 86400;
        },
        .julian_feb29 => return sp.days *% 86400,
        .mwd => {
            if (sp.month < 1 or sp.month > 12) return 0;
            const m1 = @rem(sp.month + 9, 12) + 1;
            const yy0 = if (sp.month <= 2) year - 1 else year;
            const yy1 = @divTrunc(yy0, 100);
            const yy2 = @rem(yy0, 100);
            var dow = @rem(@divTrunc(26 * m1 - 2, 10) + 1 + yy2 + @divTrunc(yy2, 4) + @divTrunc(yy1, 4) - 2 * yy1, 7);
            if (dow < 0) dow += 7;
            var d = sp.dow - dow;
            if (d < 0) d += 7;
            var i: i64 = 1;
            while (i < sp.week) : (i += 1) {
                if (d + 7 >= posix_month_lengths[leap][@intCast(sp.month - 1)]) break;
                d += 7;
            }
            var value = d * 86400;
            var k: usize = 0;
            while (k < @as(usize, @intCast(sp.month - 1))) : (k += 1) value += posix_month_lengths[leap][k] * 86400;
            return value;
        },
    }
}

fn tsAtStartOfYear(year: i64) i64 {
    const leaps = struct {
        fn f(y0: i64) i64 {
            const y = y0 - 1;
            return @divTrunc(y, 4) - @divTrunc(y, 100) + @divTrunc(y, 400);
        }
    }.f;
    return 86400 *% ((year -% 1970) *% 365 +% leaps(year) -% leaps(1970));
}

const PosixPeriod = struct { offset: i64, is_dst: bool, abbr: []const u8, transition: i64 };

// the period of the footer rule that holds `ts`; `last` is the table's final
// transition, which a rule without dst keeps as its start
fn posixLookup(rule: PosixRule, ts: i64, last: i64) ?PosixPeriod {
    const begin = rule.begin orelse return .{ .offset = rule.std_offset, .is_dst = false, .abbr = rule.std_name, .transition = last };
    const end = rule.end orelse return null;
    var y: i64 = undefined;
    var m: i64 = undefined;
    var d: i64 = undefined;
    dp.dateFromEpochDays(@divFloor(ts, 86400), &y, &m, &d);
    var times: [6]i64 = undefined;
    var dst: [6]bool = undefined;
    var n: usize = 0;
    var year = y - 1;
    while (year <= y + 1) : (year += 1) {
        const start = tsAtStartOfYear(year);
        const tb = start +% posixTransition(begin, year) +% begin.hour -% rule.std_offset;
        const te = start +% posixTransition(end, year) +% end.hour -% rule.dst_offset;
        if (tb < te) {
            times[n] = tb;
            dst[n] = true;
            times[n + 1] = te;
            dst[n + 1] = false;
        } else {
            times[n] = te;
            dst[n] = false;
            times[n + 1] = tb;
            dst[n + 1] = true;
        }
        n += 2;
    }
    var i: usize = 1;
    while (i < n) : (i += 1) {
        if (ts < times[i]) {
            const is_dst = dst[i - 1];
            return .{
                .offset = if (is_dst) rule.dst_offset else rule.std_offset,
                .is_dst = is_dst,
                .abbr = if (is_dst) rule.dst_name else rule.std_name,
                .transition = times[i - 1],
            };
        }
    }
    return null;
}

// the TZif v2+ footer text between its newlines, or null
fn tzifFooter(bytes: []const u8) ?[]const u8 {
    if (bytes.len < 44 or !std.mem.eql(u8, bytes[0..4], "TZif") or (bytes[4] != '2' and bytes[4] != '3' and bytes[4] != '4')) return null;
    const rdU32 = struct {
        fn f(b: []const u8, off: usize) usize {
            return std.mem.readInt(u32, b[off..][0..4], .big);
        }
    }.f;
    const v1 = rdU32(bytes, 32) * 5 + rdU32(bytes, 36) * 6 + rdU32(bytes, 40) + rdU32(bytes, 28) * 8 + rdU32(bytes, 24) + rdU32(bytes, 20);
    const h = 44 + v1;
    if (h + 44 > bytes.len or !std.mem.eql(u8, bytes[h .. h + 4], "TZif")) return null;
    const v2 = rdU32(bytes, h + 32) * 9 + rdU32(bytes, h + 36) * 6 + rdU32(bytes, h + 40) + rdU32(bytes, h + 28) * 12 + rdU32(bytes, h + 24) + rdU32(bytes, h + 20);
    const start = h + 44 + v2;
    if (start >= bytes.len or bytes[start] != '\n') return null;
    const end = std.mem.indexOfScalarPos(u8, bytes, start + 1, '\n') orelse return null;
    return bytes[start + 1 .. end];
}

// resolve offset/isdst/abbrev in effect at a UTC timestamp from TZif bytes.
// supports v1 (32-bit) and v2/v3 (64-bit, preferred). null on malformed input.
fn tzifLookupUtc(bytes: []const u8, utc_ts: i64) ?TzifRes {
    if (bytes.len < 44 or !std.mem.eql(u8, bytes[0..4], "TZif")) return null;
    const ver = bytes[4];
    const rdU32 = struct {
        fn f(b: []const u8, off: usize) usize {
            return std.mem.readInt(u32, b[off..][0..4], .big);
        }
    }.f;
    const v1_isutcnt = rdU32(bytes, 20);
    const v1_isstdcnt = rdU32(bytes, 24);
    const v1_leapcnt = rdU32(bytes, 28);
    const v1_timecnt = rdU32(bytes, 32);
    const v1_typecnt = rdU32(bytes, 36);
    const v1_charcnt = rdU32(bytes, 40);

    var timecnt = v1_timecnt;
    var typecnt = v1_typecnt;
    var charcnt = v1_charcnt;
    var base: usize = 44;
    var time_size: usize = 4;

    if (ver == '2' or ver == '3') {
        const v1_data = v1_timecnt * 4 + v1_timecnt + v1_typecnt * 6 + v1_charcnt + v1_leapcnt * 8 + v1_isstdcnt + v1_isutcnt;
        const v2_hdr = 44 + v1_data;
        if (v2_hdr + 44 > bytes.len or !std.mem.eql(u8, bytes[v2_hdr .. v2_hdr + 4], "TZif")) return null;
        timecnt = rdU32(bytes, v2_hdr + 32);
        typecnt = rdU32(bytes, v2_hdr + 36);
        charcnt = rdU32(bytes, v2_hdr + 40);
        base = v2_hdr + 44;
        time_size = 8;
    }
    if (typecnt == 0) return null;

    const times_off = base;
    const types_off = times_off + timecnt * time_size;
    const ttinfo_off = types_off + timecnt;
    const abbrev_off = ttinfo_off + typecnt * 6;
    if (abbrev_off + charcnt > bytes.len) return null;

    const readTime = struct {
        fn f(b: []const u8, off: usize, sz: usize) i64 {
            return if (sz == 8) std.mem.readInt(i64, b[off..][0..8], .big) else @as(i64, std.mem.readInt(i32, b[off..][0..4], .big));
        }
    }.f;

    // largest transition <= utc_ts
    var lo: usize = 0;
    var hi: usize = timecnt;
    while (lo < hi) {
        const mid = lo + (hi - lo) / 2;
        if (readTime(bytes, times_off + mid * time_size, time_size) <= utc_ts) lo = mid + 1 else hi = mid;
    }
    if (lo == timecnt and time_size == 8) {
        if (tzifFooter(bytes)) |footer| if (parsePosixRule(footer)) |rule| {
            const last = if (timecnt > 0) readTime(bytes, times_off + (timecnt - 1) * time_size, time_size) else std.math.minInt(i64);
            if (posixLookup(rule, utc_ts, last)) |period| {
                var res = TzifRes{ .offset = @intCast(period.offset), .is_dst = period.is_dst };
                const n = @min(period.abbr.len, res.abbrev_buf.len);
                @memcpy(res.abbrev_buf[0..n], period.abbr[0..n]);
                res.abbrev_len = @intCast(n);
                return res;
            }
        };
    }
    const type_idx: u8 = if (lo == 0) firstNonDstType(bytes, ttinfo_off, typecnt) else bytes[types_off + (lo - 1)];
    if (type_idx >= typecnt) return null;
    const ti = ttinfo_off + @as(usize, type_idx) * 6;
    var res = TzifRes{ .offset = std.mem.readInt(i32, bytes[ti..][0..4], .big), .is_dst = bytes[ti + 4] != 0 };
    const abbrind: usize = bytes[ti + 5];
    var i = abbrev_off + abbrind;
    while (i < abbrev_off + charcnt and bytes[i] != 0 and res.abbrev_len < res.abbrev_buf.len) : (i += 1) {
        res.abbrev_buf[res.abbrev_len] = bytes[i];
        res.abbrev_len += 1;
    }
    return res;
}

// wall-clock (naive local) -> resolved offset. Around a forward transition the
// missing interval is interpreted with the old offset, which shifts the wall
// time forward by the size of the gap. During a backward transition PHP's
// default is the offset in effect when the naive wall timestamp is treated as
// UTC. Outside transition windows one refinement resolves the ordinary case.
fn tzifLookupWall(bytes: []const u8, wall_ts: i64) ?TzifRes {
    const g0 = tzifLookupUtc(bytes, wall_ts) orelse return null;
    if (bytes.len < 44 or !std.mem.eql(u8, bytes[0..4], "TZif")) return g0;

    const rdU32 = struct {
        fn f(b: []const u8, off: usize) usize {
            return std.mem.readInt(u32, b[off..][0..4], .big);
        }
    }.f;
    const v1_isutcnt = rdU32(bytes, 20);
    const v1_isstdcnt = rdU32(bytes, 24);
    const v1_leapcnt = rdU32(bytes, 28);
    const v1_timecnt = rdU32(bytes, 32);
    const v1_typecnt = rdU32(bytes, 36);
    const v1_charcnt = rdU32(bytes, 40);
    var timecnt = v1_timecnt;
    var base: usize = 44;
    var time_size: usize = 4;
    if (bytes[4] == '2' or bytes[4] == '3') {
        const v1_data = v1_timecnt * 4 + v1_timecnt + v1_typecnt * 6 + v1_charcnt + v1_leapcnt * 8 + v1_isstdcnt + v1_isutcnt;
        const v2_hdr = 44 + v1_data;
        if (v2_hdr + 44 > bytes.len or !std.mem.eql(u8, bytes[v2_hdr .. v2_hdr + 4], "TZif")) return g0;
        timecnt = rdU32(bytes, v2_hdr + 32);
        base = v2_hdr + 44;
        time_size = 8;
    }
    if (base + timecnt * time_size > bytes.len) return g0;

    const readTime = struct {
        fn f(b: []const u8, off: usize, size: usize) i64 {
            return if (size == 8) std.mem.readInt(i64, b[off..][0..8], .big) else @as(i64, std.mem.readInt(i32, b[off..][0..4], .big));
        }
    }.f;
    var lo: usize = 0;
    var hi = timecnt;
    while (lo < hi) {
        const mid = lo + (hi - lo) / 2;
        if (readTime(bytes, base + mid * time_size, time_size) <= wall_ts) lo = mid + 1 else hi = mid;
    }
    const first = lo -| 2;
    const last = @min(timecnt, lo + 2);
    for (first..last) |i| {
        const transition = readTime(bytes, base + i * time_size, time_size);
        const before = tzifLookupUtc(bytes, transition - 1) orelse continue;
        const after = tzifLookupUtc(bytes, transition) orelse continue;
        const local_before = transition + before.offset;
        const local_after = transition + after.offset;
        if (after.offset > before.offset and wall_ts >= local_before and wall_ts < local_after) return before;
        if (after.offset < before.offset and wall_ts >= local_after and wall_ts < local_before) return g0;
    }

    return tzifLookupUtc(bytes, wall_ts - g0.offset) orelse g0;
}

// table-first, TZif-fallback. these replace the bare `lookupTimezone(name) ->
// tzOffsetAt(tz, ts)` pattern at the call sites so non-table zones work too.
pub fn tzOffsetForName(allocator: Allocator, name: []const u8, utc_ts: i64) i32 {
    if (abbrZone(name)) |hit| return @intCast(hit.offset);
    if (resolveTzif(allocator, name)) |h| {
        defer h.deinit(allocator);
        if (tzifLookupUtc(h.bytes, utc_ts)) |r| return r.offset;
    }
    if (lookupTimezone(name)) |tz| return tzOffsetAt(tz, utc_ts);
    return 0;
}

pub fn tzIsDstForName(allocator: Allocator, name: []const u8, utc_ts: i64) bool {
    if (abbrZone(name)) |hit| return hit.dst;
    if (resolveTzif(allocator, name)) |h| {
        defer h.deinit(allocator);
        if (tzifLookupUtc(h.bytes, utc_ts)) |r| return r.is_dst;
    }
    if (lookupTimezone(name)) |tz| return isDst(utc_ts, tz);
    return false;
}

pub fn tzOffsetForWallByName(allocator: Allocator, name: []const u8, wall_ts: i64) i32 {
    if (abbrZone(name)) |hit| return @intCast(hit.offset);
    if (resolveTzif(allocator, name)) |h| {
        defer h.deinit(allocator);
        if (tzifLookupWall(h.bytes, wall_ts)) |r| return r.offset;
    }
    if (lookupTimezone(name)) |tz| return tzOffsetForWall(tz, wall_ts);
    return 0;
}

// resolve the abbreviation for a zone+timestamp into an owned slice (caller
// frees), or null if unknown. table abbrevs are static; TZif ones are duped
// returns a freshly-allocated abbrev (caller frees) so table + TZif cases are
// uniform, or null if unknown
pub fn tzAbbrevForName(allocator: Allocator, name: []const u8, utc_ts: i64) ?[]const u8 {
    if (abbrZone(name) != null) return std.ascii.allocUpperString(allocator, name) catch null;
    if (resolveTzif(allocator, name)) |h| {
        defer h.deinit(allocator);
        if (tzifLookupUtc(h.bytes, utc_ts)) |r| {
            return allocator.dupe(u8, r.abbrev_buf[0..r.abbrev_len]) catch null;
        }
    }
    if (lookupTimezone(name)) |tz| return allocator.dupe(u8, tzAbbrevAt(tz, utc_ts)) catch null;
    return null;
}

// is this a zone zphp can resolve (table, system zoneinfo, or embedded blob)?
pub fn tzIsKnown(allocator: Allocator, name: []const u8) bool {
    if (lookupTimezone(name) != null) return true;
    if (resolveTzif(allocator, name)) |h| {
        defer h.deinit(allocator);
        return tzifLookupUtc(h.bytes, 0) != null;
    }
    return false;
}

fn parseFixedOffset(s: []const u8) ?i32 {
    if (s.len < 5 or (s[0] != '+' and s[0] != '-')) return null;
    const sign: i32 = if (s[0] == '-') -1 else 1;
    if (s.len >= 6 and s[3] == ':') {
        const h = std.fmt.parseInt(i32, s[1..3], 10) catch return null;
        const m = std.fmt.parseInt(i32, s[4..6], 10) catch return null;
        // "+05:30:15", as php names an offset with seconds
        const sec = if (s.len >= 9 and s[6] == ':') std.fmt.parseInt(i32, s[7..9], 10) catch return null else 0;
        return sign * (h * 3600 + m * 60 + sec);
    }
    const h = std.fmt.parseInt(i32, s[1..3], 10) catch return null;
    const m = std.fmt.parseInt(i32, s[3..5], 10) catch return null;
    return sign * (h * 3600 + m * 60);
}

// find nth occurrence of target_dow (0=sun) in given month/year, or last if n=5
fn nthWeekday(year: i64, month: i64, n: u8, target_dow: u8) i64 {
    if (n == 5) {
        // last occurrence
        const last_day = daysInMonth(month, year);
        var day = last_day;
        while (day >= 1) : (day -= 1) {
            const ts = dateToTimestamp(year, month, day, 0, 0, 0);
            const dow: u8 = @intCast(@mod(@divFloor(ts, 86400) + 4, 7));
            if (dow == target_dow) return day;
        }
        return 1;
    }
    var count: u8 = 0;
    var day: i64 = 1;
    const last_day = daysInMonth(month, year);
    while (day <= last_day) : (day += 1) {
        const ts = dateToTimestamp(year, month, day, 0, 0, 0);
        const dow: u8 = @intCast(@mod(@divFloor(ts, 86400) + 4, 7));
        if (dow == target_dow) {
            count += 1;
            if (count == n) return day;
        }
    }
    return 1;
}

fn isDst(utc_ts: i64, tz: TzEntry) bool {
    if (tz.dst_rule == .none) return false;
    const comps = baseComponents(utc_ts);
    const year = comps.year;

    if (tz.dst_rule == .us) {
        // 2:00 local on second Sunday of March -> 2:00 local on first Sunday of November.
        // local 2:00 = UTC 2:00 - std_offset (subtracting a negative offset adds hours).
        const march_day = nthWeekday(year, 3, 2, 0);
        const nov_day = nthWeekday(year, 11, 1, 0);
        const dst_start = dateToTimestamp(year, 3, march_day, 2, 0, 0) - @as(i64, tz.std_offset);
        const dst_end = dateToTimestamp(year, 11, nov_day, 2, 0, 0) - @as(i64, tz.dst_offset);
        return utc_ts >= dst_start and utc_ts < dst_end;
    }

    if (tz.dst_rule == .eu) {
        // 1:00 UTC on last Sunday of March -> 1:00 UTC on last Sunday of October
        const march_day = nthWeekday(year, 3, 5, 0);
        const oct_day = nthWeekday(year, 10, 5, 0);
        const dst_start = dateToTimestamp(year, 3, march_day, 1, 0, 0);
        const dst_end = dateToTimestamp(year, 10, oct_day, 1, 0, 0);
        return utc_ts >= dst_start and utc_ts < dst_end;
    }

    if (tz.dst_rule == .au or tz.dst_rule == .nz) {
        // southern hemisphere: DST is active in the SUMMER, which wraps the
        // year boundary - from a spring start (this calendar year) through the
        // end of the year, AND from the start of the year through an autumn end.
        // AU (NSW/VIC/ACT/TAS): 02:00 std first Sunday October -> 03:00 dst first
        // Sunday April. NZ: 02:00 std last Sunday September -> 03:00 dst first
        // Sunday April. local 02:00 std = UTC 02:00 - std_offset
        const start_month: u8 = if (tz.dst_rule == .nz) 9 else 10;
        const start_nth: u8 = if (tz.dst_rule == .nz) 5 else 1; // NZ last Sun, AU first Sun
        const start_day = nthWeekday(year, start_month, start_nth, 0);
        const apr_day = nthWeekday(year, 4, 1, 0);
        const dst_start = dateToTimestamp(year, start_month, start_day, 2, 0, 0) - @as(i64, tz.std_offset);
        const dst_end = dateToTimestamp(year, 4, apr_day, 3, 0, 0) - @as(i64, tz.dst_offset);
        return utc_ts >= dst_start or utc_ts < dst_end;
    }

    return false;
}

pub fn tzOffsetAt(tz: TzEntry, utc_ts: i64) i32 {
    if (isDst(utc_ts, tz)) return tz.dst_offset;
    return tz.std_offset;
}

// resolve the local offset for a wall-clock-naive timestamp (seconds as if
// the local wall clock were UTC). probe DST first: if interpreting the wall
// time as a DST-local moment lands inside the DST window, use the DST
// offset; otherwise fall back to standard. this picks the earlier
// interpretation for ambiguous fall-back times (01:00-02:00 local) and
// shifts forward for missing spring-forward times (02:00-03:00 local),
// matching PHP
pub fn tzOffsetForWall(tz: TzEntry, wall_naive_ts: i64) i32 {
    if (tz.dst_rule != .none) {
        if (isDst(wall_naive_ts - tz.dst_offset, tz)) return tz.dst_offset;
    }
    return tz.std_offset;
}

fn tzAbbrevAt(tz: TzEntry, utc_ts: i64) []const u8 {
    if (isDst(utc_ts, tz)) return tz.dst_abbrev;
    return tz.std_abbrev;
}

fn parseTimezoneOffset(s: []const u8) ?i64 {
    // signed offset: +H, +HH, +HHMM, +H:MM, +HH:MM (and minus variants)
    if (s.len >= 2 and (s[0] == '+' or s[0] == '-')) {
        const sign: i64 = if (s[0] == '-') -1 else 1;
        const rest = s[1..];
        if (std.mem.indexOf(u8, rest, ":")) |colon| {
            const h = std.fmt.parseInt(i64, rest[0..colon], 10) catch return null;
            const m = std.fmt.parseInt(i64, rest[colon + 1 ..], 10) catch return null;
            return sign * (h * 3600 + m * 60);
        }
        if (rest.len == 4) {
            const h = std.fmt.parseInt(i64, rest[0..2], 10) catch return null;
            const m = std.fmt.parseInt(i64, rest[2..4], 10) catch return null;
            return sign * (h * 3600 + m * 60);
        }
        const h = std.fmt.parseInt(i64, rest, 10) catch return null;
        return sign * h * 3600;
    }
    // 'GMT+N', 'GMT-N', 'GMT+HH:MM' - PHP accepts these as offset names
    // (UTC+N is NOT accepted; bare UTC is a named zone)
    if (s.len > 3 and std.mem.eql(u8, s[0..3], "GMT") and (s[3] == '+' or s[3] == '-')) {
        return parseTimezoneOffset(s[3..]);
    }
    // named: look up in table
    if (lookupTimezone(s)) |tz| {
        return @intCast(tz.std_offset);
    }
    // short abbreviations for backwards compat
    const abbrevs = [_]struct { name: []const u8, offset: i64 }{
        .{ .name = "utc", .offset = 0 },
        .{ .name = "gmt", .offset = 0 },
        .{ .name = "est", .offset = -5 * 3600 },
        .{ .name = "edt", .offset = -4 * 3600 },
        .{ .name = "cst", .offset = -6 * 3600 },
        .{ .name = "cdt", .offset = -5 * 3600 },
        .{ .name = "mst", .offset = -7 * 3600 },
        .{ .name = "mdt", .offset = -6 * 3600 },
        .{ .name = "pst", .offset = -8 * 3600 },
        .{ .name = "pdt", .offset = -7 * 3600 },
    };
    for (abbrevs) |z| {
        // exact match only - prefix matching wrongly accepted 'UTC+9' as 'UTC'
        if (s.len == z.name.len and eqlLower(s, z.name)) return z.offset;
    }
    return null;
}

fn startsWith(s: []const u8, prefix: []const u8) bool {
    return s.len >= prefix.len and std.mem.eql(u8, s[0..prefix.len], prefix);
}

// gmdate is identical to date since zphp timestamps are always UTC
fn native_gmdate(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.literal("");
    const format = args[0].string.bytes();
    const timestamp: i64 = if (args.len >= 2) Value.toInt(args[1]) else std.time.timestamp();
    return formatTimestampTz(ctx, timestamp, format, 0, "UTC");
}

fn native_tz_set(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.scalar(.{ .bool = false });
    const name = args[0].string.bytes();
    // a zone id, never an offset; php keeps the spelling it was given
    const valid = name.len > 0 and name.len <= ctx.vm.default_tz_buf.len and name[0] != '+' and name[0] != '-' and tzIsKnown(ctx.allocator, name);
    if (!valid) {
        const msg = try std.fmt.allocPrint(ctx.allocator, "date_default_timezone_set(): Timezone ID '{s}' is invalid", .{name});
        defer ctx.allocator.free(msg);
        try ctx.vm.raiseError(8, msg);
        return NativeResult.scalar(.{ .bool = false });
    }
    @memcpy(ctx.vm.default_tz_buf[0..name.len], name);
    ctx.vm.default_tz_name = ctx.vm.default_tz_buf[0..name.len];
    return NativeResult.scalar(.{ .bool = true });
}

fn native_timezone_name_get(_: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 1 or args[0] != .object) return NativeResult.scalar(.{ .bool = false });
    const name = args[0].object.get("timezone");
    if (name == .string) return NativeResult.shareString(name.string);
    return NativeResult.literal("UTC");
}

fn native_timezone_offset_get(_: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .object or args[1] != .object) return NativeResult.scalar(.{ .bool = false });
    const tz_obj = args[0].object;
    const dt_obj = args[1].object;
    const tz_name = if (tz_obj.get("timezone") == .string) tz_obj.get("timezone").string.bytes() else "UTC";
    const ts = getTimestamp(dt_obj);
    if (lookupTimezone(tz_name)) |tz| return NativeResult.scalar(.{ .int = @intCast(tzOffsetAt(tz, ts)) });
    return NativeResult.scalar(.{ .int = 0 });
}

fn native_timezone_open(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 1 or args[0] != .string) return NativeResult.scalar(.{ .bool = false });
    const zone = (try timezoneInitialize(ctx, args[0].string.bytes(), "timezone_open(): ", false)) orelse return NativeResult.scalar(.{ .bool = false });
    const obj = try ctx.createObject("DateTimeZone");
    try obj.setCopiedString(ctx.allocator, "timezone", zone.name());
    return NativeResult.borrowed(.{ .object = obj });
}

fn native_date_timezone_get(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 1 or args[0] != .object) return NativeResult.scalar(.{ .bool = false });
    const tz_v = args[0].object.get("__timezone");
    if (tz_v != .string) return NativeResult.scalar(.{ .bool = false });
    const tz_obj = try ctx.createObject("DateTimeZone");
    try tz_obj.set(ctx.allocator, "timezone", tz_v);
    return NativeResult.borrowed(.{ .object = tz_obj });
}

fn native_date_timezone_set(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len < 2 or args[0] != .object or args[1] != .object) return NativeResult.scalar(.{ .bool = false });
    const tz_name = args[1].object.get("timezone");
    if (tz_name == .string) {
        try args[0].object.set(ctx.allocator, "__timezone", tz_name);
    }
    return NativeResult.share(args[0]);
}

fn native_tz_get(ctx: *NativeContext, _: []const Value) RuntimeError!NativeResult {
    return try NativeResult.copyString(ctx.allocator, ctx.vm.default_tz_name);
}

// the broken-down local time of a timestamp in a zone, as localtime() and
// idate() report it
const LocalParts = struct {
    dc: DateComponents,
    offset: i32,
    dst: bool,
    dow: i64, // 0 = sunday
    yday: i64,
    day_num: i64,
    local_ts: i64,
};

fn localParts(a: Allocator, timestamp: i64, tz_name: []const u8) LocalParts {
    const offset = tzOffsetForName(a, tz_name, timestamp);
    const local_ts = timestamp + offset;
    const dc = baseComponents(local_ts);
    const day_num = @divFloor(local_ts, 86400);
    return .{
        .dc = dc,
        .offset = offset,
        .dst = tzIsDstForName(a, tz_name, timestamp),
        .dow = @mod(day_num + 4, 7),
        .yday = day_num - @divFloor(dateToTimestamp(dc.year, 1, 1, 0, 0, 0), 86400),
        .day_num = day_num,
        .local_ts = local_ts,
    };
}

fn native_localtime(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const timestamp: i64 = if (args.len >= 1 and args[0] != .null) Value.toInt(args[0]) else std.time.timestamp();
    const assoc = args.len >= 2 and args[1].isTruthy();
    const lp = localParts(ctx.allocator, timestamp, ctx.vm.default_tz_name);
    const fields = [_]struct { []const u8, i64 }{
        .{ "tm_sec", lp.dc.sec },
        .{ "tm_min", lp.dc.min },
        .{ "tm_hour", lp.dc.hour },
        .{ "tm_mday", lp.dc.day },
        .{ "tm_mon", lp.dc.month - 1 },
        .{ "tm_year", lp.dc.year - 1900 },
        .{ "tm_wday", lp.dow },
        .{ "tm_yday", lp.yday },
        .{ "tm_isdst", @intFromBool(lp.dst) },
    };
    var arr = try ctx.createArray();
    for (fields) |f| {
        if (assoc) {
            try arr.set(ctx.allocator, .{ .string = Value.String.borrowed(f[0]) }, .{ .int = f[1] });
        } else {
            try arr.append(ctx.allocator, .{ .int = f[1] });
        }
    }
    return NativeResult.borrowed(.{ .array = arr });
}

fn native_idate(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string or args[0].string.bytes().len == 0) return NativeResult.scalar(.{ .bool = false });
    const fmt = args[0].string.bytes()[0];
    const timestamp: i64 = if (args.len >= 2 and args[1] != .null) Value.toInt(args[1]) else std.time.timestamp();
    const lp = localParts(ctx.allocator, timestamp, ctx.vm.default_tz_name);
    const dc = lp.dc;

    const v: i64 = switch (fmt) {
        'd' => dc.day,
        'h' => if (@mod(dc.hour, 12) == 0) 12 else @mod(dc.hour, 12),
        'H' => dc.hour,
        'i' => dc.min,
        'm' => dc.month,
        's' => dc.sec,
        // php hands idate's result through a c int
        'U' => @as(i32, @truncate(timestamp)),
        'w' => lp.dow,
        'y' => @mod(dc.year, 100),
        'Y' => dc.year,
        't' => daysInMonth(dc.month, dc.year),
        'z' => lp.yday,
        'I' => @intFromBool(lp.dst),
        'L' => @intFromBool(isLeapYear(dc.year)),
        'N' => if (lp.dow == 0) 7 else lp.dow,
        'o' => isoWeek(lp.day_num).year,
        'W' => isoWeek(lp.day_num).week,
        // swatch beats are measured in utc+1
        'B' => @divFloor(@mod(timestamp + 3600, 86400) * 10, 864),
        'Z' => lp.offset,
        else => return NativeResult.scalar(.{ .bool = false }),
    };
    return NativeResult.scalar(.{ .int = v });
}

fn isLeapYear(y: i64) bool {
    return (@mod(y, 4) == 0 and @mod(y, 100) != 0) or @mod(y, 400) == 0;
}

const IsoDuration = struct { y: i64, m: i64, d: i64, h: i64, mi: i64, s: i64, f: f64 };

fn parseIsoDuration(spec: []const u8) IsoDuration {
    var result = IsoDuration{ .y = 0, .m = 0, .d = 0, .h = 0, .mi = 0, .s = 0, .f = 0 };
    if (spec.len == 0 or spec[0] != 'P') return result;
    var in_time = false;
    var num_start: ?usize = null;
    for (spec[1..], 1..) |c, idx| {
        if (c >= '0' and c <= '9' or c == '.') {
            if (num_start == null) num_start = idx;
        } else if (c == 'T') {
            in_time = true;
            num_start = null;
        } else if (num_start) |ns| {
            const num_str = spec[ns..idx];
            if (std.mem.indexOf(u8, num_str, ".")) |_| {
                const val = std.fmt.parseFloat(f64, num_str) catch 0.0;
                if (in_time and c == 'S') {
                    result.s = Value.dvalToLval(val);
                    result.f = val - @as(f64, @floatFromInt(result.s));
                }
            } else {
                const val = std.fmt.parseInt(i64, num_str, 10) catch 0;
                if (!in_time) {
                    switch (c) {
                        'Y' => result.y = val,
                        'M' => result.m = val,
                        'D' => result.d = val,
                        'W' => result.d = val * 7,
                        else => {},
                    }
                } else {
                    switch (c) {
                        'H' => result.h = val,
                        'M' => result.mi = val,
                        'S' => result.s = val,
                        else => {},
                    }
                }
            }
            num_start = null;
        }
    }
    return result;
}

fn diConstruct(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len > 0 and args[0] == .string) {
        const dur = parseIsoDuration(args[0].string.bytes());
        try obj.set(ctx.allocator, "y", .{ .int = dur.y });
        try obj.set(ctx.allocator, "m", .{ .int = dur.m });
        try obj.set(ctx.allocator, "d", .{ .int = dur.d });
        try obj.set(ctx.allocator, "h", .{ .int = dur.h });
        try obj.set(ctx.allocator, "i", .{ .int = dur.mi });
        try obj.set(ctx.allocator, "s", .{ .int = dur.s });
        try obj.set(ctx.allocator, "f", .{ .float = dur.f });
    }
    try obj.set(ctx.allocator, "invert", .{ .int = 0 });
    try obj.set(ctx.allocator, "days", .{ .bool = false });
    return NativeResult.scalar(.null);
}

fn diCreateFromDateString(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    if (args.len == 0 or args[0] != .string) return NativeResult.scalar(.{ .bool = false });
    const dur = parseRelativeDuration(args[0].string.bytes());
    const obj = try ctx.createObject("DateInterval");
    try obj.set(ctx.allocator, "y", .{ .int = dur.y });
    try obj.set(ctx.allocator, "m", .{ .int = dur.m });
    try obj.set(ctx.allocator, "d", .{ .int = dur.d });
    try obj.set(ctx.allocator, "h", .{ .int = dur.h });
    try obj.set(ctx.allocator, "i", .{ .int = dur.mi });
    try obj.set(ctx.allocator, "s", .{ .int = dur.s });
    try obj.set(ctx.allocator, "f", .{ .float = 0 });
    try obj.set(ctx.allocator, "invert", .{ .int = 0 });
    try obj.set(ctx.allocator, "days", .{ .bool = false });
    return NativeResult.borrowed(.{ .object = obj });
}

const RelDuration = struct { y: i64 = 0, m: i64 = 0, d: i64 = 0, h: i64 = 0, mi: i64 = 0, s: i64 = 0 };

fn parseRelativeDuration(input: []const u8) RelDuration {
    var out = RelDuration{};
    var i: usize = 0;
    while (i < input.len) {
        while (i < input.len and (input[i] == ' ' or input[i] == '\t')) : (i += 1) {}
        if (i >= input.len) break;

        // optional sign
        var sign: i64 = 1;
        if (input[i] == '+' or input[i] == '-') {
            if (input[i] == '-') sign = -1;
            i += 1;
        }

        // digits
        const num_start = i;
        while (i < input.len and isDigit(input[i])) : (i += 1) {}
        if (i == num_start) {
            // not a number — skip a token to make progress
            while (i < input.len and input[i] != ' ' and input[i] != '\t') : (i += 1) {}
            continue;
        }
        const value = sign * (std.fmt.parseInt(i64, input[num_start..i], 10) catch 0);

        // optional whitespace before unit
        while (i < input.len and (input[i] == ' ' or input[i] == '\t')) : (i += 1) {}

        // unit
        const unit_start = i;
        while (i < input.len and isAlpha(input[i])) : (i += 1) {}
        const unit = input[unit_start..i];
        if (unit.len == 0) continue;

        // check for trailing " ago" which inverts the value
        var save_i = i;
        while (save_i < input.len and (input[save_i] == ' ' or input[save_i] == '\t')) : (save_i += 1) {}
        var v = value;
        if (save_i + 3 <= input.len and eqlLower(input[save_i .. save_i + 3], "ago")) {
            v = -v;
            i = save_i + 3;
        }

        if (matchUnit(unit, "year")) out.y += v else if (matchUnit(unit, "month")) out.m += v else if (matchUnit(unit, "week")) out.d += v * 7 else if (matchUnit(unit, "day")) out.d += v else if (matchUnit(unit, "hour")) out.h += v else if (matchUnit(unit, "minute") or matchUnit(unit, "min")) out.mi += v else if (matchUnit(unit, "second") or matchUnit(unit, "sec")) out.s += v;
    }
    return out;
}

fn matchUnit(actual: []const u8, base: []const u8) bool {
    if (eqlLower(actual, base)) return true;
    if (actual.len == base.len + 1 and (actual[actual.len - 1] == 's' or actual[actual.len - 1] == 'S') and eqlLower(actual[0..base.len], base)) return true;
    return false;
}

fn diFormat(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len == 0 or args[0] != .string) return NativeResult.literal("");
    const fmt = args[0].string.bytes();

    // raw signed values (PHP's lowercase %d/%h/etc print signed values for
    // intervals created from date strings); uppercase variants use abs/2-digit
    const y_signed = Value.toInt(obj.get("y"));
    const m_signed = Value.toInt(obj.get("m"));
    const d_signed = Value.toInt(obj.get("d"));
    const h_signed = Value.toInt(obj.get("h"));
    const i_signed = Value.toInt(obj.get("i"));
    const s_signed = Value.toInt(obj.get("s"));
    const y: u64 = @intCast(@abs(y_signed));
    const m: u64 = @intCast(@abs(m_signed));
    const d: u64 = @intCast(@abs(d_signed));
    const h: u64 = @intCast(@abs(h_signed));
    const mi: u64 = @intCast(@abs(i_signed));
    const s: u64 = @intCast(@abs(s_signed));
    const f_us: u64 = blk: {
        const fv = obj.get("f");
        if (fv == .float) {
            // diff() stores whole microseconds as f, which can come back a
            // hair under the integer (0.250001 * 1e6 = 250000.99...)
            const us: i64 = Value.dvalToLval(@round(fv.float * 1_000_000.0));
            break :blk @intCast(@abs(us));
        }
        break :blk 0;
    };
    const days_v = obj.get("days");
    const has_days = days_v == .int;
    const days_total: u64 = if (has_days) @intCast(@abs(days_v.int)) else 0;
    const invert = Value.toInt(obj.get("invert")) != 0;

    var buf = std.array_list.Managed(u8).init(ctx.allocator);
    defer buf.deinit();
    const w = buf.writer();

    var i: usize = 0;
    while (i < fmt.len) : (i += 1) {
        const c = fmt[i];
        if (c != '%' or i + 1 >= fmt.len) {
            try w.writeByte(c);
            continue;
        }
        i += 1;
        switch (fmt[i]) {
            'Y' => try w.print("{d:0>2}", .{y}),
            'y' => try w.print("{d}", .{y_signed}),
            'M' => try w.print("{d:0>2}", .{m}),
            'm' => try w.print("{d}", .{m_signed}),
            'D' => try w.print("{d:0>2}", .{d}),
            'd' => try w.print("{d}", .{d_signed}),
            'a' => if (has_days) try w.print("{d}", .{days_total}) else try w.writeAll("(unknown)"),
            'H' => try w.print("{d:0>2}", .{h}),
            'h' => try w.print("{d}", .{h_signed}),
            'I' => try w.print("{d:0>2}", .{mi}),
            'i' => try w.print("{d}", .{i_signed}),
            'S' => try w.print("{d:0>2}", .{s}),
            's' => try w.print("{d}", .{s_signed}),
            'F' => try w.print("{d:0>6}", .{f_us}),
            'f' => try w.print("{d}", .{f_us}),
            'R' => try w.writeByte(if (invert) '-' else '+'),
            'r' => if (invert) try w.writeByte('-'),
            '%' => try w.writeByte('%'),
            else => {
                try w.writeByte('%');
                try w.writeByte(fmt[i]);
            },
        }
    }

    return NativeResult.takeString(try Value.String.adopt(ctx.allocator, try buf.toOwnedSlice()));
}

fn diInvert(ctx: *NativeContext, args: []const Value) RuntimeError!NativeResult {
    const obj = getThis(ctx) orelse return NativeResult.scalar(.null);
    if (args.len > 0) {
        const invert = if (args[0] == .bool) (if (args[0].bool) @as(i64, 1) else @as(i64, 0)) else if (args[0] == .int) (if (args[0].int != 0) @as(i64, 1) else @as(i64, 0)) else @as(i64, 0);
        try obj.set(ctx.allocator, "invert", .{ .int = invert });
    }
    return NativeResult.borrowed(.{ .object = obj });
}

// ---- the date string parser (date_parse.zig) wired to zphp's zones ----

const dp = @import("date_parse.zig");

// the transition in effect at a utc instant, the way timelib looks it up:
// the period's offset, when it began (minInt before the first transition),
// and whether it is dst
fn tzifOffsetInfo(bytes: []const u8, utc_ts: i64) ?dp.OffsetInfo {
    if (bytes.len < 44 or !std.mem.eql(u8, bytes[0..4], "TZif")) return null;
    const rdU32 = struct {
        fn f(b: []const u8, off: usize) usize {
            return std.mem.readInt(u32, b[off..][0..4], .big);
        }
    }.f;
    var timecnt = rdU32(bytes, 32);
    var typecnt = rdU32(bytes, 36);
    var charcnt = rdU32(bytes, 40);
    var base: usize = 44;
    var time_size: usize = 4;
    if (bytes[4] == '2' or bytes[4] == '3') {
        const v1_data = timecnt * 4 + timecnt + typecnt * 6 + charcnt + rdU32(bytes, 28) * 8 + rdU32(bytes, 24) + rdU32(bytes, 20);
        const v2_hdr = 44 + v1_data;
        if (v2_hdr + 44 > bytes.len or !std.mem.eql(u8, bytes[v2_hdr .. v2_hdr + 4], "TZif")) return null;
        timecnt = rdU32(bytes, v2_hdr + 32);
        typecnt = rdU32(bytes, v2_hdr + 36);
        charcnt = rdU32(bytes, v2_hdr + 40);
        base = v2_hdr + 44;
        time_size = 8;
    }
    if (typecnt == 0) return null;
    const types_off = base + timecnt * time_size;
    const ttinfo_off = types_off + timecnt;
    if (ttinfo_off + typecnt * 6 > bytes.len) return null;
    const readTime = struct {
        fn f(b: []const u8, off: usize, sz: usize) i64 {
            return if (sz == 8) std.mem.readInt(i64, b[off..][0..8], .big) else @as(i64, std.mem.readInt(i32, b[off..][0..4], .big));
        }
    }.f;
    var lo: usize = 0;
    var hi: usize = timecnt;
    while (lo < hi) {
        const mid = lo + (hi - lo) / 2;
        if (readTime(bytes, base + mid * time_size, time_size) <= utc_ts) lo = mid + 1 else hi = mid;
    }
    if (lo == timecnt and time_size == 8) {
        if (tzifFooter(bytes)) |footer| if (parsePosixRule(footer)) |rule| {
            const last = if (timecnt > 0) readTime(bytes, base + (timecnt - 1) * time_size, time_size) else std.math.minInt(i64);
            if (posixLookup(rule, utc_ts, last)) |period| return .{ .offset = period.offset, .transition_time = period.transition, .is_dst = period.is_dst };
        };
    }
    var transition: i64 = std.math.minInt(i64);
    var type_idx: usize = undefined;
    if (lo == 0) {
        type_idx = firstNonDstType(bytes, ttinfo_off, typecnt);
    } else {
        transition = readTime(bytes, base + (lo - 1) * time_size, time_size);
        type_idx = bytes[types_off + (lo - 1)];
    }
    if (type_idx >= typecnt) return null;
    const ti = ttinfo_off + type_idx * 6;
    return .{
        .offset = std.mem.readInt(i32, bytes[ti..][0..4], .big),
        .transition_time = transition,
        .is_dst = bytes[ti + 4] != 0,
    };
}

// a zone id is matched case-insensitively and keeps the spelling it was given,
// as timelib_tzinfo_ctor does
fn dbLookupId(name: []const u8, out: *[64]u8) ?[]const u8 {
    if (name.len == 0 or name.len > out.len) return null;
    if (embeddedZone(name, true) == null) {
        if (std.mem.eql(u8, name, "localtime") or std.mem.eql(u8, name, "posixrules")) return null;
        if (!tzNameValid(name) or !tzIsKnown(std.heap.page_allocator, name)) return null;
    }
    @memcpy(out[0..name.len], name);
    return out[0..name.len];
}

fn dbOffsetInfo(id: []const u8, ts: i64) ?dp.OffsetInfo {
    if (resolveTzif(std.heap.page_allocator, id)) |h| {
        if (tzifOffsetInfo(h.bytes, ts)) |r| return r;
    }
    if (lookupTimezone(id)) |tz| return .{ .offset = tzOffsetAt(tz, ts), .transition_time = std.math.minInt(i64), .is_dst = isDst(ts, tz) };
    return null;
}

const parse_db = dp.TzDb{ .lookupId = dbLookupId, .offsetInfo = dbOffsetInfo };

// an abbreviation zone ("EDT", "CEST", "Z"): php reads a name shorter than six
// characters as an abbreviation before trying it as a zone id, and keeps its
// offset and dst flag fixed. exact "UTC" stays the zone id
fn abbrZone(name: []const u8) ?dp.AbbrHit {
    if (name.len == 0 or name.len >= dp.MAX_ABBR_LEN or std.mem.eql(u8, name, "UTC")) return null;
    if (name[0] == '+' or name[0] == '-') return null;
    return dp.abbrSearch(name);
}

// a zone as php's timelib_time carries it: an id, a fixed offset, or an
// abbreviation (std offset plus a dst flag)
const ParseZone = struct {
    kind: u8 = dp.ZONETYPE_ID,
    buf: [64]u8 = undefined,
    len: u8 = 0,
    z: i64 = 0,
    dst: i64 = 0,

    fn name(self: *const ParseZone) []const u8 {
        return self.buf[0..self.len];
    }

    fn setName(self: *ParseZone, s: []const u8) void {
        const n = @min(s.len, self.buf.len);
        @memcpy(self.buf[0..n], s[0..n]);
        self.len = @intCast(n);
    }

    // the zone a stored name denotes; the default zone is always an id
    fn of(n: []const u8, force_id: bool) ParseZone {
        var zone = ParseZone{};
        zone.setName(n);
        if (force_id) return zone;
        if (n.len > 0 and (n[0] == '+' or n[0] == '-')) {
            zone.kind = dp.ZONETYPE_OFFSET;
            zone.z = if (parseFixedOffset(n)) |o| o else if (parseTimezoneOffset(n)) |o| o else 0;
        } else if (abbrZone(n)) |hit| {
            zone.kind = dp.ZONETYPE_ABBR;
            zone.dst = if (hit.dst) 1 else 0;
            zone.z = hit.offset - zone.dst * 3600;
        }
        return zone;
    }

    // the zone a resolved time ended up in, named the way php's getName() does
    fn ofTime(t: *const dp.Time) ParseZone {
        var zone = ParseZone{ .kind = t.zone_type, .z = t.z, .dst = t.dst };
        switch (t.zone_type) {
            dp.ZONETYPE_OFFSET => {
                var b: [16]u8 = undefined;
                zone.setName(offsetName(&b, t.z));
            },
            dp.ZONETYPE_ABBR => zone.setName(t.abbr() orelse "UTC"),
            else => zone.setName(t.id() orelse "UTC"),
        }
        return zone;
    }

    fn totalOffset(self: *const ParseZone, a: Allocator, ts: i64) i64 {
        return switch (self.kind) {
            dp.ZONETYPE_OFFSET => self.z,
            dp.ZONETYPE_ABBR => self.z + self.dst * 3600,
            else => tzOffsetForName(a, self.name(), ts),
        };
    }
};

// "+05:30" (and "+05:30:15" for an offset with seconds)
fn offsetName(buf: *[16]u8, z: i64) []const u8 {
    const sign: u8 = if (z < 0) '-' else '+';
    const abs: u64 = @abs(z);
    const h = abs / 3600;
    const m = (abs % 3600) / 60;
    const s = abs % 60;
    if (s != 0) return std.fmt.bufPrint(buf, "{c}{d:0>2}:{d:0>2}:{d:0>2}", .{ sign, h, m, s }) catch "+00:00";
    return std.fmt.bufPrint(buf, "{c}{d:0>2}:{d:0>2}", .{ sign, h, m }) catch "+00:00";
}

// wall-clock fields of an instant in a zone (timelib_unixtime2local)
fn wallTime(a: Allocator, ts: i64, us: i64, zone: *const ParseZone) dp.Time {
    var t = dp.Time{ .sse = ts };
    const off = zone.totalOffset(a, ts);
    const wall = ts +% off;
    const days = @divFloor(wall, 86400);
    const secs = wall - days * 86400;
    dp.dateFromEpochDays(days, &t.y, &t.m, &t.d);
    t.h = @divTrunc(secs, 3600);
    t.i = @divTrunc(@rem(secs, 3600), 60);
    t.s = @rem(secs, 60);
    t.us = us;
    t.zone_type = zone.kind;
    t.is_localtime = true;
    t.have_zone = 1;
    switch (zone.kind) {
        dp.ZONETYPE_OFFSET => {
            t.z = zone.z;
            t.dst = 0;
        },
        dp.ZONETYPE_ABBR => {
            t.z = zone.z;
            t.dst = zone.dst;
            t.setAbbr(zone.name());
        },
        else => {
            t.z = off;
            t.dst = if (tzIsDstForName(a, zone.name(), ts)) 1 else 0;
            t.setId(zone.name());
        },
    }
    return t;
}

fn nowOf(a: Allocator, ts: i64, us: i64, zone: *const ParseZone) dp.Now {
    const t = wallTime(a, ts, us, zone);
    return .{ .y = t.y, .m = t.m, .d = t.d, .h = t.h, .i = t.i, .s = t.s, .us = us, .z = t.z, .dst = t.dst, .zone_type = zone.kind, .name = zone.name() };
}

fn parseDate(ctx: *NativeContext, input: []const u8) RuntimeError!dp.Parsed {
    return dp.parse(ctx.allocator, input, parse_db) catch return error.OutOfMemory;
}

fn recordLastErrors(ctx: *NativeContext, p: *const dp.Parsed) void {
    var rec = vm_mod.DtLastErrors{};
    for (p.warnings.items) |w| rec.addWarning(w.pos, w.msg);
    for (p.errors.items) |e| rec.addError(e.pos, e.msg);
    rec.set = rec.warning_count > 0 or rec.error_count > 0;
    ctx.vm.last_dt = rec;
}

// "Failed to parse time string (%s) at position %d (%c): %s", with an optional
// "Class::method(): " prefix
fn parseFailure(ctx: *NativeContext, prefix: []const u8, input: []const u8, p: *const dp.Parsed) RuntimeError![]u8 {
    const e = p.errors.items[0];
    const c: u8 = if (e.char == 0) ' ' else e.char;
    return std.fmt.allocPrint(ctx.allocator, "{s}Failed to parse time string ({s}) at position {d} ({c}): {s}", .{ prefix, input, e.pos, c, e.msg }) catch error.OutOfMemory;
}

fn currentTimeMicros() struct { ts: i64, us: i64 } {
    const ns = std.time.nanoTimestamp();
    return .{ .ts = @intCast(@divFloor(ns, 1_000_000_000)), .us = @intCast(@divFloor(@mod(ns, 1_000_000_000), 1_000)) };
}

// zone names live for the process: there are few distinct ones, and date
// objects then share one borrowed string instead of copying it per object
var zone_names: std.StringHashMapUnmanaged(void) = .{};
var zone_names_mutex: std.Thread.Mutex = .{};
threadlocal var last_zone_name_interned: []const u8 = "";

fn internZoneName(name: []const u8) []const u8 {
    if (std.mem.eql(u8, name, last_zone_name_interned)) return last_zone_name_interned;
    zone_names_mutex.lock();
    defer zone_names_mutex.unlock();
    const kept = if (zone_names.getKey(name)) |k| k else blk: {
        const copy = std.heap.page_allocator.dupe(u8, name) catch return "UTC";
        zone_names.put(std.heap.page_allocator, copy, {}) catch return "UTC";
        break :blk copy;
    };
    last_zone_name_interned = kept;
    return kept;
}

fn storeInstant(ctx: *NativeContext, obj: *PhpObject, ts: i64, us: i64, zone: *const ParseZone) RuntimeError!void {
    try obj.set(ctx.allocator, "timestamp", .{ .int = ts });
    const cur_zone = obj.get("__timezone");
    if (cur_zone != .string or !std.mem.eql(u8, cur_zone.string.bytes(), zone.name())) {
        try obj.set(ctx.allocator, "__timezone", .{ .string = Value.String.borrowed(internZoneName(zone.name())) });
    }
    // a missing __microseconds reads as 0
    if (us != 0 or obj.get("__microseconds") != .null) try obj.set(ctx.allocator, "__microseconds", .{ .int = us });
}

// php_date_initialize: the constructors and date_create(). a parse error
// throws when `throws` is set (the constructors) and otherwise only makes the
// call fail. tz_arg is the DateTimeZone argument's zone name
const Instant = struct { ts: i64, us: i64, zone: ParseZone };

fn resolveDate(ctx: *NativeContext, input_arg: []const u8, tz_arg: ?[]const u8, throws: bool) RuntimeError!?Instant {
    const input = if (input_arg.len == 0) "now" else input_arg;
    var p = try parseDate(ctx, input);
    defer p.deinit(ctx.allocator);
    recordLastErrors(ctx, &p);
    if (p.errors.items.len > 0) {
        if (throws) {
            const msg = try parseFailure(ctx, "", input, &p);
            defer ctx.allocator.free(msg);
            try ctx.vm.setPendingException("DateMalformedStringException", msg);
        }
        return null;
    }
    const zone = if (tz_arg) |n|
        ParseZone.of(n, false)
    else if (p.time.zone_type == dp.ZONETYPE_ID and p.time.id() != null)
        ParseZone.of(p.time.id().?, true)
    else
        ParseZone.of(ctx.vm.default_tz_name, true);
    const now_t = currentTimeMicros();
    if (std.mem.eql(u8, input, "now")) return .{ .ts = now_t.ts, .us = now_t.us, .zone = zone };
    const now = nowOf(ctx.allocator, now_t.ts, now_t.us, &zone);
    dp.fillHoles(&p.time, &now, false);
    dp.updateTs(&p.time, if (zone.kind == dp.ZONETYPE_ID) zone.name() else null, parse_db);
    return .{ .ts = p.time.sse, .us = p.time.us, .zone = ParseZone.ofTime(&p.time) };
}

// php_date_modify: the parsed fields replace the object's wall-clock fields,
// then the relative part applies. a parsed zone is ignored, except that
// "@ts" moves the object to +00:00
fn modifyDateObject(ctx: *NativeContext, obj: *PhpObject, input: []const u8, prefix: []const u8, throws: bool) RuntimeError!bool {
    var p = try parseDate(ctx, input);
    defer p.deinit(ctx.allocator);
    recordLastErrors(ctx, &p);
    if (p.errors.items.len > 0) {
        const msg = try parseFailure(ctx, prefix, input, &p);
        defer ctx.allocator.free(msg);
        if (throws) {
            try ctx.vm.setPendingException("DateMalformedStringException", msg);
            return error.RuntimeError;
        }
        try ctx.vm.emitWarning(msg);
        return false;
    }
    const zone = ParseZone.of(objTzName(obj, ctx.vm.default_tz_name), false);
    const us_v = obj.get("__microseconds");
    var t = wallTime(ctx.allocator, getTimestamp(obj), if (us_v == .int) us_v.int else 0, &zone);
    const tmp = &p.time;
    t.relative = tmp.relative;
    t.have_relative = tmp.have_relative;
    if (tmp.y != dp.UNSET) t.y = tmp.y;
    if (tmp.m != dp.UNSET) t.m = tmp.m;
    if (tmp.d != dp.UNSET) t.d = tmp.d;
    if (tmp.h != dp.UNSET) {
        t.h = tmp.h;
        if (tmp.i != dp.UNSET) {
            t.i = tmp.i;
            t.s = if (tmp.s != dp.UNSET) tmp.s else 0;
        } else {
            t.i = 0;
            t.s = 0;
        }
    }
    if (tmp.us != dp.UNSET) t.us = tmp.us;
    if (tmp.y == 1970 and tmp.m == 1 and tmp.d == 1 and tmp.h == 0 and tmp.i == 0 and tmp.s == 0 and tmp.us == 0 and
        tmp.have_zone != 0 and tmp.zone_type == dp.ZONETYPE_OFFSET and tmp.z == 0 and tmp.dst == 0)
    {
        t.zone_type = dp.ZONETYPE_OFFSET;
        t.z = 0;
        t.dst = 0;
        t.has_id = false;
        t.has_abbr = false;
    }
    dp.updateTs(&t, null, parse_db);
    const result_zone = ParseZone.ofTime(&t);
    try storeInstant(ctx, obj, t.sse, t.us, &result_zone);
    return true;
}

// strtotime(): unset fields come from the base time in the default zone
fn strtotimeIn(ctx: *NativeContext, input: []const u8, base: i64) RuntimeError!?i64 {
    var p = try parseDate(ctx, input);
    defer p.deinit(ctx.allocator);
    if (p.errors.items.len > 0) return null;
    const zone = ParseZone.of(ctx.vm.default_tz_name, true);
    const now = nowOf(ctx.allocator, base, 0, &zone);
    dp.fillHoles(&p.time, &now, false);
    dp.updateTs(&p.time, zone.name(), parse_db);
    return p.time.sse;
}

fn putAssoc(ctx: *NativeContext, arr: *@import("../runtime/value.zig").PhpArray, key: []const u8, v: Value) RuntimeError!void {
    try arr.set(ctx.allocator, .{ .string = Value.String.borrowed(key) }, v);
}

fn putAssocStr(ctx: *NativeContext, arr: *@import("../runtime/value.zig").PhpArray, key: []const u8, bytes: []const u8) RuntimeError!void {
    const str = try Value.String.create(ctx.allocator, bytes);
    defer str.release();
    try putAssoc(ctx, arr, key, .{ .string = str });
}

fn messagesArray(ctx: *NativeContext, msgs: []const vm_mod.DtMsg) RuntimeError!*@import("../runtime/value.zig").PhpArray {
    const arr = try ctx.createArray();
    for (msgs) |m| try arr.set(ctx.allocator, .{ .int = @intCast(m.pos) }, .{ .string = Value.String.borrowed(m.msg) });
    return arr;
}

fn putErrorContainer(ctx: *NativeContext, arr: *@import("../runtime/value.zig").PhpArray, rec: *const vm_mod.DtLastErrors) RuntimeError!void {
    try putAssoc(ctx, arr, "warning_count", .{ .int = rec.warning_count });
    try putAssoc(ctx, arr, "warnings", .{ .array = try messagesArray(ctx, rec.warnings[0..rec.warnings_len]) });
    try putAssoc(ctx, arr, "error_count", .{ .int = rec.error_count });
    try putAssoc(ctx, arr, "errors", .{ .array = try messagesArray(ctx, rec.errors[0..rec.errors_len]) });
}

// php_date_do_return_parsed_time
fn parsedTimeArray(ctx: *NativeContext, p: *const dp.Parsed) RuntimeError!NativeResult {
    const t = &p.time;
    const arr = try ctx.createArray();
    const field = struct {
        fn f(v: i64) Value {
            return if (v == dp.UNSET) .{ .bool = false } else .{ .int = v };
        }
    }.f;
    try putAssoc(ctx, arr, "year", field(t.y));
    try putAssoc(ctx, arr, "month", field(t.m));
    try putAssoc(ctx, arr, "day", field(t.d));
    try putAssoc(ctx, arr, "hour", field(t.h));
    try putAssoc(ctx, arr, "minute", field(t.i));
    try putAssoc(ctx, arr, "second", field(t.s));
    try putAssoc(ctx, arr, "fraction", if (t.us == dp.UNSET) .{ .bool = false } else .{ .float = @as(f64, @floatFromInt(t.us)) / 1000000.0 });
    var rec = vm_mod.DtLastErrors{};
    for (p.warnings.items) |w| rec.addWarning(w.pos, w.msg);
    for (p.errors.items) |e| rec.addError(e.pos, e.msg);
    try putErrorContainer(ctx, arr, &rec);
    try putAssoc(ctx, arr, "is_localtime", .{ .bool = t.is_localtime });
    if (t.is_localtime) {
        try putAssoc(ctx, arr, "zone_type", .{ .int = t.zone_type });
        switch (t.zone_type) {
            dp.ZONETYPE_OFFSET => {
                try putAssoc(ctx, arr, "zone", field(t.z));
                try putAssoc(ctx, arr, "is_dst", .{ .bool = t.dst != 0 });
            },
            dp.ZONETYPE_ID => {
                if (t.abbr()) |ab| try putAssocStr(ctx, arr, "tz_abbr", ab);
                if (t.id()) |id| try putAssocStr(ctx, arr, "tz_id", id);
            },
            dp.ZONETYPE_ABBR => {
                try putAssoc(ctx, arr, "zone", field(t.z));
                try putAssoc(ctx, arr, "is_dst", .{ .bool = t.dst != 0 });
                try putAssocStr(ctx, arr, "tz_abbr", t.abbr() orelse "");
            },
            else => {},
        }
    }
    if (t.have_relative) {
        const rel = try ctx.createArray();
        const r = &t.relative;
        try putAssoc(ctx, rel, "year", .{ .int = r.y });
        try putAssoc(ctx, rel, "month", .{ .int = r.m });
        try putAssoc(ctx, rel, "day", .{ .int = r.d });
        try putAssoc(ctx, rel, "hour", .{ .int = r.h });
        try putAssoc(ctx, rel, "minute", .{ .int = r.i });
        try putAssoc(ctx, rel, "second", .{ .int = r.s });
        if (r.have_weekday_relative) try putAssoc(ctx, rel, "weekday", .{ .int = r.weekday });
        if (r.have_special_relative and r.special_type == dp.SPECIAL_WEEKDAY) try putAssoc(ctx, rel, "weekdays", .{ .int = r.special_amount });
        if (r.first_last_day_of != 0) try putAssoc(ctx, rel, if (r.first_last_day_of == dp.FIRST_DAY_OF_MONTH) "first_day_of_month" else "last_day_of_month", .{ .bool = true });
        try putAssoc(ctx, arr, "relative", .{ .array = rel });
    }
    return NativeResult.borrowed(.{ .array = arr });
}
